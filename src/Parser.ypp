%{
  #include <sstream>
  
  #include "wisey/AccessLevel.hpp"
  #include "wisey/AdditiveMultiplicativeExpression.hpp"
  #include "wisey/AdjustByExpression.hpp"
  #include "wisey/ArrayAllocation.hpp"
  #include "wisey/ArrayAllocationStatic.hpp"
  #include "wisey/ArrayElementExpression.hpp"
  #include "wisey/ArrayOwnerTypeSpecifier.hpp"
  #include "wisey/ArraySpecificOwnerTypeSpecifier.hpp"
  #include "wisey/ArraySpecificTypeSpecifier.hpp"
  #include "wisey/ArrayTypeSpecifier.hpp"
  #include "wisey/Assignment.hpp"
  #include "wisey/BindAction.hpp"
  #include "wisey/BindActionGlobalStatement.hpp"
  #include "wisey/BitwiseAndExpression.hpp"
  #include "wisey/Block.hpp"
  #include "wisey/BooleanConstant.hpp"
  #include "wisey/BooleanNotExpression.hpp"
  #include "wisey/BreakStatement.hpp"
  #include "wisey/BuilderArgument.hpp"
  #include "wisey/Catch.hpp"
  #include "wisey/CaseStatement.hpp"
  #include "wisey/CastExpression.hpp"
  #include "wisey/CharConstant.hpp"
  #include "wisey/CompoundStatement.hpp"
  #include "wisey/ConditionalExpression.hpp"
  #include "wisey/ConstantDefinition.hpp"
  #include "wisey/ConstantReference.hpp"
  #include "wisey/ContinueStatement.hpp"
  #include "wisey/ControllerDefinition.hpp"
  #include "wisey/ControllerTypeSpecifier.hpp"
  #include "wisey/ControllerTypeSpecifierFull.hpp"
  #include "wisey/DefaultCaseStatement.hpp"
  #include "wisey/DereferenceExpression.hpp"
  #include "wisey/DoStatement.hpp"
  #include "wisey/DoubleConstant.hpp"
  #include "wisey/EmptyExpression.hpp"
  #include "wisey/EmptyStatement.hpp"
  #include "wisey/ExpressionStatement.hpp"
  #include "wisey/ExternalConstantDeclaration.hpp"
  #include "wisey/ExternalControllerDefinition.hpp"
  #include "wisey/ExternalInterfaceDefinition.hpp"
  #include "wisey/ExternalMethodDefinition.hpp"
  #include "wisey/ExternalModelDefinition.hpp"
  #include "wisey/ExternalNodeDefinition.hpp"
  #include "wisey/ExternalStaticMethodDefinition.hpp"
  #include "wisey/FakeExpression.hpp"
  #include "wisey/FloatConstant.hpp"
  #include "wisey/ForStatement.hpp"
  #include "wisey/HeapBuilder.hpp"
  #include "wisey/IBuildableObjectTypeSpecifier.hpp"
  #include "wisey/IControllerTypeSpecifier.hpp"
  #include "wisey/IGlobalStatement.hpp"
  #include "wisey/IInterfaceTypeSpecifier.hpp"
  #include "wisey/ILLVMTypeSpecifier.hpp"
  #include "wisey/IModelTypeSpecifier.hpp"
  #include "wisey/INodeTypeSpecifier.hpp"
  #include "wisey/IObjectElementDefinition.hpp"
  #include "wisey/IObjectDefinition.hpp"
  #include "wisey/IObjectTypeSpecifier.hpp"
  #include "wisey/ITypeSpecifier.hpp"
  #include "wisey/Identifier.hpp"
  #include "wisey/IdentifierChain.hpp"
  #include "wisey/IdentifierReference.hpp"
  #include "wisey/IfElseStatement.hpp"
  #include "wisey/IfStatement.hpp"
  #include "wisey/ImmutableArrayOwnerTypeSpecifier.hpp"
  #include "wisey/ImmutableArrayTypeSpecifier.hpp"
  #include "wisey/ImportStatement.hpp"
  #include "wisey/IncrementExpression.hpp"
  #include "wisey/InjectedFieldDefinition.hpp"
  #include "wisey/InjectionArgument.hpp"
  #include "wisey/Injector.hpp"
  #include "wisey/IntConstant.hpp"
  #include "wisey/InterfaceDefinition.hpp"
  #include "wisey/InterfaceTypeSpecifier.hpp"
  #include "wisey/InterfaceTypeSpecifierFull.hpp"
  #include "wisey/LLVMArrayTypeSpecifer.hpp"
  #include "wisey/LLVMExternalStructDefinition.hpp"
  #include "wisey/LLVMGlobalDeclaration.hpp"
  #include "wisey/LLVMFunctionCall.hpp"
  #include "wisey/LLVMFunctionDeclaration.hpp"
  #include "wisey/LLVMFunctionDefinition.hpp"
  #include "wisey/LLVMFunctionIdentifier.hpp"
  #include "wisey/LLVMFunctionTypeSpecifier.hpp"
  #include "wisey/LLVMPointerOwnerTypeSpecifier.hpp"
  #include "wisey/LLVMPointerTypeSpecifier.hpp"
  #include "wisey/LLVMPrimitiveTypes.hpp"
  #include "wisey/LLVMStructDefinition.hpp"
  #include "wisey/LLVMStructSpecifier.hpp"
  #include "wisey/LLVMVariableDeclaration.hpp"
  #include "wisey/Log.hpp"
  #include "wisey/LogicalAndExpression.hpp"
  #include "wisey/LogicalOrExpression.hpp"
  #include "wisey/LongConstant.hpp"
  #include "wisey/MethodCall.hpp"
  #include "wisey/MethodDefinition.hpp"
  #include "wisey/MethodQualifier.hpp"
  #include "wisey/MethodSignatureDeclaration.hpp"
  #include "wisey/ModelDefinition.hpp"
  #include "wisey/ModelTypeSpecifier.hpp"
  #include "wisey/ModelTypeSpecifierFull.hpp"
  #include "wisey/NegateExpression.hpp"
  #include "wisey/NodeDefinition.hpp"
  #include "wisey/NodeTypeSpecifier.hpp"
  #include "wisey/NodeTypeSpecifierFull.hpp"
  #include "wisey/NullExpression.hpp"
  #include "wisey/ObjectOwnerTypeSpecifier.hpp"
  #include "wisey/PackageType.hpp"
  #include "wisey/PoolBuilder.hpp"
  #include "wisey/PrimitiveTypes.hpp"
  #include "wisey/PrimitiveTypeSpecifier.hpp"
  #include "wisey/PrintErrStatement.hpp"
  #include "wisey/PrintFileStatement.hpp"
  #include "wisey/PrintOutStatement.hpp"
  #include "wisey/ProgramFile.hpp"
  #include "wisey/ReceivedFieldDefinition.hpp"
  #include "wisey/RelationalExpression.hpp"
  #include "wisey/ReturnStatement.hpp"
  #include "wisey/ReturnVoidStatement.hpp"
  #include "wisey/ShiftLeftExpression.hpp"
  #include "wisey/ShiftRightExpression.hpp"
  #include "wisey/StringLiteral.hpp"
  #include "wisey/StateFieldDefinition.hpp"
  #include "wisey/StaticMethodCall.hpp"
  #include "wisey/StaticMethodDefinition.hpp"
  #include "wisey/StoreInReference.hpp"
  #include "wisey/SwitchStatement.hpp"
  #include "wisey/TryCatchStatement.hpp"
  #include "wisey/ThrowStatement.hpp"
  #include "wisey/TypeComparisionExpression.hpp"
  #include "wisey/VariableDeclaration.hpp"
  #include "wisey/WiseyModelOwnerTypeSpecifier.hpp"
  #include "wisey/WiseyModelTypeSpecifier.hpp"
  #include "wisey/WiseyObjectOwnerTypeSpecifier.hpp"
  #include "wisey/WiseyObjectTypeSpecifier.hpp"
  #include "wisey/WhileStatement.hpp"
  
  using namespace std;
  using namespace wisey;
  
  ProgramFile *programFile;
  PackageType* filePackageType;
  
  extern int yylex();
  extern int yylineno;
  extern std::string SourceFile;

  #define YYDEBUG 1

  void yyerror(const char *error_msg) {
    printf("%s(%d): %s\n", SourceFile.c_str(), yylineno, error_msg);
    exit(1);
  }
%}

%error-verbose
%locations

/** 
 * Data types used for parsing
 */
%union {
  wisey::IExpression* expression;
  wisey::IStatement* statement;
  wisey::IGlobalStatement* global_statement;
  wisey::GlobalStatementList* global_statement_list;
  wisey::Block* block;
  wisey::BindAction* bind_action;
  wisey::CompoundStatement* compound_statement;
  wisey::CaseStatement* case_statement;
  wisey::DefaultCaseStatement* default_case_statement;
  wisey::SwitchCases* switch_cases;
  const wisey::ITypeSpecifier* type_specifier;
  wisey::ArrayTypeSpecifier* array_type_specifier;
  wisey::ArrayOwnerTypeSpecifier* array_owner_type_specifier;
  wisey::ArraySpecificTypeSpecifier* array_specific_type_specifier;
  wisey::ImmutableArrayTypeSpecifier* immutable_array_type_specifier;
  wisey::ImmutableArrayOwnerTypeSpecifier* immutable_array_owner_type_specifier;
  wisey::IObjectDefinition* object_definition;
  wisey::IObjectTypeSpecifier* object_type_specifier;
  wisey::IBuildableObjectTypeSpecifier* object_type_specifier_buildable;
  wisey::Identifier* identifier;
  wisey::VariableDeclaration* variable_declaration;
  wisey::BuilderArgument* builder_argument;
  wisey::BuilderArgumentList* builder_argument_list;
  wisey::InjectionArgument* injection_argument;
  wisey::InjectionArgumentList* injection_argument_list;
  wisey::IModelTypeSpecifier* model_type_specifier;
  wisey::IControllerTypeSpecifier* controller_type_specifier;
  wisey::IInterfaceTypeSpecifier* interface_type_specifier;
  wisey::IObjectElementDefinition* object_element;
  wisey::INodeTypeSpecifier* node_type_specifier;
  wisey::AccessLevel access_specifier;
  wisey::MethodSignatureDeclaration* method_signature;
  wisey::Catch* catch_clause;
  wisey::ProgramFile* program_file;
  const wisey::ILLVMTypeSpecifier* llvm_type_specifier;
  wisey::LLVMPointerTypeSpecifier* llvm_pointer_type_specifier;
  wisey::LLVMPointerOwnerTypeSpecifier* llvm_pointer_owner_type_specifier;
  wisey::LLVMVariableDeclaration* llvm_variable_declaration;
  wisey::MethodQualifier method_qualifier;
  std::vector<wisey::Catch*>* catch_vector;
  std::vector<wisey::VariableDeclaration*>* variable_vector;
  std::vector<const wisey::IExpression*>* expression_vector;
  std::vector<wisey::MethodSignatureDeclaration*>* method_signature_vector;
  std::tuple<std::vector<wisey::IObjectElementDefinition*>,
             std::vector<wisey::IObjectDefinition*>>* object_element_list;
  std::vector<wisey::ITypeSpecifier*>* type_specifier_list;
  std::vector<wisey::IModelTypeSpecifier*>* model_type_specifier_list;
  std::vector<wisey::IInterfaceTypeSpecifier*>* interface_list;
  std::list<const wisey::IExpression*>* list_of_const_expressions;
  std::list<unsigned long>* list_of_ulongs;
  std::vector<const wisey::ITypeSpecifier*>* const_type_specifier_list;
  std::vector<wisey::LLVMVariableDeclaration*>* llvm_variable_vector;
  wisey::MethodQualifiers* method_qualifiers;
  std::string* string;
  unsigned long unsignedlong;
  int token;
  int integer;
}

/**
 * Tokens used in wisey 
 */
%token <string> TIDENTIFIER TCHAR TINTEGER TLONG TFLOAT TDOUBLE TSTRING_LITERAL TMODELIDENTIFIER
%token <string> TFIELDIDENTIFIER TINTERFACEIDENTIFIER TCONTROLLERIDENTIFIER TNODEIDENTIFIER
%token <string> TCONSTANTIDENTIFIER

%token <token> TANDOP TBIND TBREAK TBUILD TCASE TCATCH TCEQ TCGE TCLE
%token <token> TCONSTANT TCONTINUE TCNE TCONTROLLER TDECOP TDECBYOP TDO TDOUBLECOLON TELIPSIS
%token <token> TELSE TEXTENDS TEXTERNAL TDEFAULT TFALLTHROUGH TFALSE TFOR TIF TIMMEDIATE TIMMUTABLE
%token <token> TIMPLEMENTS TIMPORT TINCOP TINCBYOP TINJECT TINSTANCEOF TINTERFACE  
%token <token> TLLVM TLLVMARRAY TLLVMDEREFERENCE TLLVMFUNCTION TLLVMI1 TLLVMI8 TLLVMI16 TLLVMI32
%token <token> TLLVMI64 TLLVMPOINTER TLLVMSTORE TLLVMSTRUCT TLLVMVOID TLLVMREFERENCE
%token <token> TMODEL TNEW TNODE TNULL TONHEAP TONPOOL TOROP TOVERRIDE TPACKAGE
%token <token> TPRINTFILE TPRINTERR TPRINTOUT TPRIVATE TPUBLIC TRECEIVE TRETURN 
%token <token> TINSCOPE TSHIFTLEFT TSHIFTRIGHT TSTATE TSTATIC TSWITCH 
%token <token> TTHROW TTHROWS TTO TTRY TTRUE TTYPEBOOLEAN 
%token <token> TTYPECHAR TTYPEDOUBLE TTYPEFLOAT TTYPEINT TTYPELONG TTYPESTRING TTYPEVOID TWHILE
%token <token> TWISEY TWISEYMODEL TWISEYOBJECT

/**
 * Parsing tree node types
 */
%type <identifier> identifier
%type <string> package_optional package_name string_identifier llvm_function_name
%type <expression> string_literal constant_value expression multiplicative_expression 
%type <expression> additive_expression relational_expression equality_expression
%type <expression> logical_and_expression logical_or_expression shift_expression
%type <expression> conditional_expression postfix_expression heap_builder pool_builder
%type <expression> cast_expression primary_expression assignment unary_expression
%type <expression> type_comparision_expression injection array_allocation static_array_allocation
%type <expression> constant_reference static_method_call field_identifier increment_by_expression
%type <expression> llvm_function_call llvm_reference llvm_dereference call_argument
%type <expression> bitwise_and_expression 
%type <variable_vector> method_definition_arguments
%type <expression_vector> call_arguments static_array_elements
%type <block> block_item_list 
%type <program_file> program
%type <statement> statement variable_declaration
%type <statement> method_argument_declaration jump_statement block_item
%type <statement> declaration expression_statement iteration_statement
%type <statement> print_statement selection_statement 
%type <statement> throw_statement try_catch_statement
%type <statement> llvm_function_argument_declaration llvm_store
%type <global_statement> bind_action_statement import_statement global_statement llvm_statement 
%type <global_statement> llvm_struct_definition llvm_function_declaration 
%type <global_statement> llvm_external_struct_definition llvm_global_declaration
%type <object_definition> controller_definition interface_definition model_definition
%type <object_definition> node_definition
%type <object_definition> object_definition external_object_definition external_model_definition
%type <object_definition> external_node_definition external_controller_definition
%type <object_definition> external_interface_definition
%type <object_definition> inner_controller_definition inner_interface_definition 
%type <object_definition> inner_model_definition inner_node_definition
%type <object_definition> inner_object_definition
%type <global_statement_list> global_statement_list
%type <compound_statement> compound_statement
%type <case_statement> case_statement
%type <default_case_statement> default_case_statement
%type <switch_cases> case_statement_list case_statement_list_with_default
%type <object_element> object_element_declaration external_object_element_declaration
%type <object_element> field_definition method_definition static_method_definition
%type <object_element> external_method_definition interface_static_method_definition
%type <object_element> constant_definition constant_declaration interface_element_declaration 
%type <object_element> method_signature external_interface_element_declaration 
%type <object_element> external_static_method_definition llvm_function_definition
%type <object_element_list> object_element_declaration_list 
%type <object_element_list> external_object_element_declaration_list
%type <object_element_list> interface_element_delcaration_list
%type <object_element_list> external_interface_element_delcaration_list
%type <array_type_specifier> array_type_specifier
%type <array_owner_type_specifier> array_owner_type_specifier
%type <immutable_array_type_specifier> immutable_array_type_specifier
%type <immutable_array_owner_type_specifier> immutable_array_owner_type_specifier
%type <array_specific_type_specifier> array_specific_type_specifier
%type <object_type_specifier> object_type_specifier
%type <object_type_specifier> object_type_specifier_dot
%type <object_type_specifier> object_type_specifier_short 
%type <object_type_specifier_buildable> object_type_specifier_buildable
%type <object_type_specifier> object_type_specifier_top_level_full
%type <object_type_specifier> object_type_specifier_injectable 
%type <object_type_specifier> object_type_specifier_inner_short
%type <object_type_specifier> object_type_specifier_inner_full
%type <object_type_specifier> object_scope
%type <builder_argument_list> builder_argument_list
%type <builder_argument> builder_argument
%type <injection_argument_list> injection_argument_list
%type <injection_argument> injection_argument
%type <bind_action> bind_action
%type <model_type_specifier> model_type_specifier
%type <model_type_specifier> model_type_specifier_top_level
%type <model_type_specifier> model_type_specifier_short
%type <model_type_specifier> exception_type_specifier 
%type <model_type_specifier> model_type_specifier_top_level_full 
%type <model_type_specifier> model_type_specifier_inner 
%type <model_type_specifier> model_type_specifier_inner_short
%type <model_type_specifier> model_type_specifier_inner_full
%type <model_type_specifier> model_type_specifier_full
%type <controller_type_specifier> controller_type_specifier 
%type <controller_type_specifier> controller_type_specifier_top_level 
%type <controller_type_specifier> controller_type_specifier_short 
%type <controller_type_specifier> controller_type_specifier_top_level_full
%type <controller_type_specifier> controller_type_specifier_inner
%type <controller_type_specifier> controller_type_specifier_inner_short
%type <controller_type_specifier> controller_type_specifier_inner_full
%type <controller_type_specifier> controller_type_specifier_full
%type <interface_type_specifier> interface_type_specifier
%type <interface_type_specifier> interface_type_specifier_top_level
%type <interface_type_specifier> interface_type_specifier_short
%type <interface_type_specifier> interface_type_specifier_top_level_full
%type <interface_type_specifier> interface_type_specifier_inner
%type <interface_type_specifier> interface_type_specifier_inner_short
%type <interface_type_specifier> interface_type_specifier_inner_full
%type <interface_type_specifier> interface_type_specifier_full
%type <node_type_specifier> node_type_specifier
%type <node_type_specifier> node_type_specifier_top_level
%type <node_type_specifier> node_type_specifier_short
%type <node_type_specifier> node_type_specifier_top_level_full
%type <node_type_specifier> node_type_specifier_inner
%type <node_type_specifier> node_type_specifier_inner_short
%type <node_type_specifier> node_type_specifier_inner_full
%type <node_type_specifier> node_type_specifier_full
%type <access_specifier> access_specifier
%type <catch_clause> catch_clause
%type <catch_vector> catch_block
%type <model_type_specifier_list> method_exceptions exception_type_specifier_list
%type <interface_list> interface_list implmeneted_interfaces extends_interfaces
%type <list_of_const_expressions> array_dimensions
%type <unsignedlong> array_undefined_dimensions_list
%type <type_specifier> type_specifier primitive_type_specifier object_owner_type_specifier 
%type <type_specifier> non_array_type_specifier injectable_type_specifier 
%type <type_specifier> array_specific_owner_type_specifier
%type <type_specifier> wisey_object_type_specifier wisey_object_owner_type_specifier
%type <type_specifier> wisey_model_type_specifier wisey_model_owner_type_specifier
%type <type_specifier> native_type_specifier llvm_type_specifier wisey_type_specifier
%type <type_specifier> llvm_non_array_type_specifier 
%type <type_specifier> llvm_void_type_specifier
%type <llvm_type_specifier> llvm_i_type_specifier llvm_struct_specifier llvm_array_specifier
%type <llvm_type_specifier> llvm_pointer_base_type_specifier llvm_function_type_specifier
%type <llvm_pointer_type_specifier> llvm_pointer_type_specifier
%type <llvm_pointer_owner_type_specifier> llvm_pointer_owner_type_specifier
%type <const_type_specifier_list> native_type_specifier_list 
%type <const_type_specifier_list> llvm_function_argument_type_specifier_list
%type <llvm_variable_vector> llvm_function_definition_arguments
%type <list_of_ulongs> llvm_array_dimensions
%type <method_qualifier> method_qualifier
%type <method_qualifiers> method_qualifiers method_qualifier_set
%type <integer> on_pool

%left '+' '-'
%left '*' '/' '%'

/**
 * Expect one shift/reduce conflit: the dangling ELSE
 */
%expect 0

/**
 * Top-most parsing tree node
 */
%start program

%%

program
  : package_optional global_statement_list
    { programFile = new ProgramFile(*$1, *$2); delete $1; delete $2; }
  ;

package_optional
  : /* blank */ { $$ = new std::string("wisey.lang"); filePackageType = new PackageType(*$$); }
  | TPACKAGE package_name ';' {  filePackageType = new PackageType(*$2); $$ = $2; }
  ;

package_name
  : TIDENTIFIER
  | package_name '.' TIDENTIFIER
    { $$ = $1; $$->append(std::string(".")); $$->append(*$3); delete $3; }
  ;

global_statement_list
  : global_statement { $$ = new GlobalStatementList(); $$->push_back($1); }
  | global_statement_list global_statement { $1->push_back($2); }
  ;

global_statement
  : object_definition { $$ = (IGlobalStatement*) $1; }
  | external_object_definition { $$ = (IGlobalStatement*) $1; }
  | bind_action_statement
  | import_statement
  | llvm_statement
  ;

llvm_statement
  : llvm_struct_definition
  | llvm_function_declaration
  | llvm_global_declaration
  | llvm_external_struct_definition
  ;

llvm_global_declaration
  : llvm_type_specifier TIDENTIFIER ';'
    { $$ = new LLVMGlobalDeclaration($1, *$2); delete $2; }
  ;

llvm_function_declaration
  : TLLVM TLLVMFUNCTION native_type_specifier llvm_function_name
    '(' llvm_function_argument_type_specifier_list ')' ';'
      {
        $$ = LLVMFunctionDeclaration::createInternal(*$4, $3, *$6, yylineno);
        delete $4;
        delete $6;
      }
  | TLLVM TLLVMFUNCTION native_type_specifier llvm_function_name '(' ')' ';'
      { 
        std::vector<const ITypeSpecifier*> typeSpecifiers;        
        $$ = LLVMFunctionDeclaration::createInternal(*$4, $3, typeSpecifiers, yylineno);
        delete $4; 
      }
  | TLLVM TLLVMFUNCTION native_type_specifier llvm_function_name
    '(' llvm_function_argument_type_specifier_list ',' TELIPSIS ')' ';'
      { 
        $$ = LLVMFunctionDeclaration::createInternalWithVarArg(*$4, $3, *$6, yylineno);
        delete $4; 
        delete $6; 
      }
  | TLLVM TLLVMFUNCTION native_type_specifier llvm_function_name
    '(' TELIPSIS ')' ';'
      { 
        std::vector<const ITypeSpecifier*> typeSpecifiers;
        $$ = LLVMFunctionDeclaration::createInternalWithVarArg(*$4, $3, typeSpecifiers, yylineno);
        delete $4; 
      }
  | TEXTERNAL TLLVM TLLVMFUNCTION native_type_specifier llvm_function_name
    '(' llvm_function_argument_type_specifier_list ')' ';'
      {
        $$ = LLVMFunctionDeclaration::createExternal(*$5, $4, *$7, yylineno);
        delete $5;
        delete $7;
      }
  | TEXTERNAL TLLVM TLLVMFUNCTION native_type_specifier llvm_function_name '(' ')' ';'
      { 
        std::vector<const ITypeSpecifier*> typeSpecifiers;
        $$ = LLVMFunctionDeclaration::createExternal(*$5, $4, typeSpecifiers, yylineno);
        delete $5; 
      }
  | TEXTERNAL TLLVM TLLVMFUNCTION native_type_specifier llvm_function_name
    '(' llvm_function_argument_type_specifier_list ',' TELIPSIS ')' ';'
      { 
        $$ = LLVMFunctionDeclaration::createExternalWithVarArg(*$5, $4, *$7, yylineno);
        delete $5; 
        delete $7;
      }
  | TEXTERNAL TLLVM TLLVMFUNCTION native_type_specifier llvm_function_name
    '(' TELIPSIS ')' ';'
      { 
        std::vector<const ITypeSpecifier*> typeSpecifiers;
        $$ = LLVMFunctionDeclaration::createExternalWithVarArg(*$5, $4, typeSpecifiers, yylineno);
        delete $5; 
      }
  ;

llvm_struct_definition
  : TLLVM TLLVMSTRUCT TIDENTIFIER '{' native_type_specifier_list '}' 
    { $$ = new LLVMStructDefinition(*$3, *$5, yylineno); delete $3; delete $5; }
  | TLLVM TLLVMSTRUCT TIDENTIFIER '{' '}' 
    { 
      std::vector<const ITypeSpecifier*> emptyList;
      $$ = new LLVMStructDefinition(*$3, emptyList, yylineno); 
      delete $3;
    }
  ;

llvm_external_struct_definition
  : TEXTERNAL TLLVM TLLVMSTRUCT TIDENTIFIER '{' native_type_specifier_list '}' 
    { $$ = new LLVMExternalStructDefinition(*$4, *$6, yylineno); delete $4; delete $6; }
  | TEXTERNAL TLLVM TLLVMSTRUCT TIDENTIFIER '{' '}' 
    { 
      std::vector<const ITypeSpecifier*> emptyList;
      $$ = new LLVMExternalStructDefinition(*$4, emptyList, yylineno); 
      delete $4; 
    }
  ;

native_type_specifier_list
  : native_type_specifier
    { $$ = new std::vector<const ITypeSpecifier*>(); $$->push_back($1); }
  | native_type_specifier_list ',' native_type_specifier
    { $1->push_back($3); $$ = $1; }
  | native_type_specifier_list ','
    { $$ = $1; }
  ;

native_type_specifier
  : llvm_type_specifier
  | wisey_type_specifier
  ;

llvm_type_specifier
  : llvm_non_array_type_specifier
  | llvm_array_specifier { $$ = (const ITypeSpecifier*) $1; }
  | llvm_pointer_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | llvm_pointer_owner_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  ;

wisey_type_specifier
  : wisey_object_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | wisey_object_owner_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | wisey_model_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | wisey_model_owner_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  ; 

wisey_object_type_specifier
  : TWISEY TWISEYOBJECT { $$ = new WiseyObjectTypeSpecifier(yylineno); }
  ;

wisey_object_owner_type_specifier
  : wisey_object_type_specifier '*'
    { $$ = new WiseyObjectOwnerTypeSpecifier(yylineno); }
  ;

wisey_model_type_specifier
  : TWISEY TWISEYMODEL { $$ = new WiseyModelTypeSpecifier(yylineno); }
  ;

wisey_model_owner_type_specifier
  : wisey_model_type_specifier '*'
    { $$ = new WiseyModelOwnerTypeSpecifier(yylineno); }
  ;

llvm_non_array_type_specifier
  : llvm_i_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | llvm_struct_specifier { $$ = (const ITypeSpecifier*) $1; }
  | llvm_function_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | llvm_void_type_specifier
  ;

llvm_pointer_owner_type_specifier
  : llvm_pointer_type_specifier '*'
    { $$ = new LLVMPointerOwnerTypeSpecifier($1); }
  ;

llvm_pointer_type_specifier
  : llvm_pointer_base_type_specifier TLLVMPOINTER
    { $$ = new LLVMPointerTypeSpecifier($1, yylineno); }
  | llvm_pointer_type_specifier TLLVMPOINTER
    { $$ = new LLVMPointerTypeSpecifier($1, yylineno); }
  ;

llvm_pointer_base_type_specifier
  : llvm_i_type_specifier
  | llvm_struct_specifier
  | llvm_function_type_specifier
  | llvm_array_specifier
  ;

llvm_i_type_specifier
  : TLLVM TLLVMI1 { $$ = LLVMPrimitiveTypes::I1->newTypeSpecifier(yylineno); }
  | TLLVM TLLVMI8 { $$ = LLVMPrimitiveTypes::I8->newTypeSpecifier(yylineno); }
  | TLLVM TLLVMI16 { $$ = LLVMPrimitiveTypes::I16->newTypeSpecifier(yylineno); }
  | TLLVM TLLVMI32 { $$ = LLVMPrimitiveTypes::I32->newTypeSpecifier(yylineno); }
  | TLLVM TLLVMI64 { $$ = LLVMPrimitiveTypes::I64->newTypeSpecifier(yylineno); }
  ;

llvm_void_type_specifier
  : TLLVM TLLVMVOID { $$ = LLVMPrimitiveTypes::VOID->newTypeSpecifier(yylineno); }

llvm_struct_specifier
  : TLLVM TLLVMSTRUCT TDOUBLECOLON TIDENTIFIER
    { $$ = new LLVMStructSpecifier(*$4, yylineno); delete $4; }
  ;

llvm_array_specifier
  : TLLVM TLLVMARRAY '(' llvm_non_array_type_specifier ',' llvm_array_dimensions ')'
    { $$ = new LLVMArrayTypeSpecifer($4, *$6, yylineno); delete $6; }
  ;

llvm_array_dimensions
  : TINTEGER
    { 
      $$ = new std::list<unsigned long>();
      $$->push_back(atol($1->c_str()));
      delete $1;
    }
  | llvm_array_dimensions ',' TINTEGER 
    { $1->push_back(atol($3->c_str())); delete $3; $$ = $1; }
  ;

llvm_function_type_specifier
  : native_type_specifier '(' llvm_function_argument_type_specifier_list ')'
    { $$ = new LLVMFunctionTypeSpecifier($1, *$3, yylineno); delete $3; }
  ;

llvm_function_argument_type_specifier_list
  : native_type_specifier
    { $$ = new std::vector<const ITypeSpecifier*>(); $$->push_back($1); }
  | llvm_function_argument_type_specifier_list ',' type_specifier
    { $1->push_back($3); $$ = $1; }
  ;

llvm_function_definition
  : access_specifier TLLVM TLLVMFUNCTION native_type_specifier TIDENTIFIER
    '(' llvm_function_definition_arguments ')' compound_statement
      { $$ = new LLVMFunctionDefinition(*$5, $1, $4, *$7, $9, yylineno); delete $5; delete $7; }
  ;

llvm_function_definition_arguments
  : /* blank */  { $$ = new LLVMVariableList(); }
  | llvm_function_argument_declaration
    { $$ = new LLVMVariableList(); $$->push_back($<llvm_variable_declaration>1); }
  | llvm_function_definition_arguments ',' llvm_function_argument_declaration
    { $$ = $1; $$->push_back($<llvm_variable_declaration>3); }
  ;

llvm_function_argument_declaration
  : type_specifier identifier { $$ = LLVMVariableDeclaration::create($1, $2, yylineno); }
  ;

llvm_function_call
  : TLLVM TLLVMFUNCTION TDOUBLECOLON llvm_function_name '(' call_arguments ')'
    {
      $$ = new LLVMFunctionCall(*$4, *$6, yylineno);
      delete $4;
      delete $6;
    }
  ;

llvm_function_name
  : string_identifier { $$ = $1; }
  | llvm_function_name '.' string_identifier 
    { $$ = new std::string(*$1 + "." + *$3); delete $1; delete $3; }
  | TSTRING_LITERAL { $$ = new std::string($1->substr(1, $1->length() - 2)); delete $1; }
  ;

llvm_reference
  : TLLVM TLLVMREFERENCE '(' TIDENTIFIER ')' 
    { $$ = new IdentifierReference(*$4, yylineno); delete $4; }
  | TLLVM TLLVMREFERENCE '(' TFIELDIDENTIFIER ')' 
    { $$ = new IdentifierReference(*$4, yylineno); delete $4; }
  ;

llvm_dereference
  : TLLVM TLLVMDEREFERENCE '(' expression ')'
    { $$ = new DereferenceExpression($4, yylineno); }
  ;

llvm_store
  : TLLVM TLLVMSTORE '(' expression ',' expression ')' ';'
    { $$ = new StoreInReference($4, $6, yylineno); }
  ;

string_identifier
  : TIDENTIFIER
  | TMODELIDENTIFIER
  | TINTERFACEIDENTIFIER
  | TCONTROLLERIDENTIFIER
  | TNODEIDENTIFIER
  | TFIELDIDENTIFIER
  ;  

inner_object_definition
  : inner_controller_definition
  | inner_interface_definition
  | inner_model_definition
  | inner_node_definition
  ;

object_definition
  : controller_definition
  | interface_definition
  | model_definition
  | node_definition
  ;

external_object_definition
  : external_controller_definition
  | external_interface_definition
  | external_model_definition
  | external_node_definition
  ;

import_statement
  : TIMPORT interface_type_specifier_full ';' { $$ = new ImportStatement($2); }
  | TIMPORT model_type_specifier_full ';' { $$ = new ImportStatement($2); }
  | TIMPORT controller_type_specifier_full ';' { $$ = new ImportStatement($2); }
  | TIMPORT node_type_specifier_full ';' { $$ = new ImportStatement($2); }
  ;

bind_action_statement
  : bind_action ';' { $$ = new BindActionGlobalStatement($1, yylineno); }
  ;

bind_action
  : TBIND '(' interface_type_specifier ')' '.' TTO '(' controller_type_specifier ')'
    { 
      InjectionArgumentList injectionArgumentList;
      $$ = new BindAction($3, $8, injectionArgumentList); 
    }
  | TBIND '(' interface_type_specifier ')' '.' TTO '(' controller_type_specifier ')' 
      injection_argument_list
    { $$ = new BindAction($3, $8, *$10); delete $10; }
  ;

interface_definition
  : TINTERFACE interface_type_specifier_top_level extends_interfaces 
      '{' interface_element_delcaration_list '}'
    {
      IExpression* package = $2->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      InterfaceTypeSpecifierFull* fullSpecifier = 
        new InterfaceTypeSpecifierFull(package, $2->getShortName(), yylineno);
      $$ = new InterfaceDefinition(AccessLevel::PUBLIC_ACCESS,
                                   fullSpecifier, 
                                   *$3, 
                                   std::get<0>(*$5), 
                                   std::get<1>(*$5),
                                   yylineno);
      delete $2;
      delete $3;
      delete $5;
    }
  ;

inner_interface_definition
  : access_specifier TINTERFACE interface_type_specifier_short extends_interfaces 
      '{' interface_element_delcaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      InterfaceTypeSpecifierFull* fullSpecifier =
        new InterfaceTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new InterfaceDefinition($1,
                                   fullSpecifier, 
                                   *$4, 
                                   std::get<0>(*$6), 
                                   std::get<1>(*$6),
                                   yylineno);
      delete $3;
      delete $4;
      delete $6;
    }
  ;

external_interface_definition
  : TEXTERNAL TINTERFACE interface_type_specifier_full extends_interfaces 
      '{' external_interface_element_delcaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      InterfaceTypeSpecifierFull* fullSpecifier =
        new InterfaceTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ExternalInterfaceDefinition(fullSpecifier,
                                           *$4,
                                           std::get<0>(*$6),
                                           std::get<1>(*$6),
                                           yylineno);
      delete $3;
      delete $4;
      delete $6;
    }
  ;

external_interface_element_delcaration_list
  : /* blank */ 
    { 
      $$ = new std::tuple<std::vector<IObjectElementDefinition*>,
                          std::vector<IObjectDefinition*>>();
    }
  | external_interface_element_delcaration_list external_interface_element_declaration
    { std::get<0>(*$1).push_back($2); }
  | external_interface_element_delcaration_list external_object_definition
    { std::get<1>(*$1).push_back($2); }
  ;

external_interface_element_declaration
  : method_signature
  | constant_declaration
  | external_static_method_definition
  ;

interface_element_delcaration_list
  : /* blank */ 
    { 
      $$ = new std::tuple<std::vector<IObjectElementDefinition*>,
                          std::vector<IObjectDefinition*>>();
    }
  | interface_element_delcaration_list interface_element_declaration
    { std::get<0>(*$1).push_back($2); }
  | interface_element_delcaration_list inner_object_definition
    { std::get<1>(*$1).push_back($2); }
  ;

interface_element_declaration
  : method_signature
  | constant_definition
  | interface_static_method_definition
  | llvm_function_definition
  ;

method_signature
  : type_specifier TIDENTIFIER '(' method_definition_arguments ')' method_exceptions 
    method_qualifiers ';'
      {
        $$ = new MethodSignatureDeclaration($1, *$2, *$4, *$6, $7, yylineno);
        delete $2;
        delete $4;
        delete $6;
      }
  ;

controller_definition
  : TCONTROLLER controller_type_specifier_top_level object_scope 
      implmeneted_interfaces '{' object_element_declaration_list '}'
    {
      IExpression* package = $2->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ControllerTypeSpecifierFull* fullSpecifier =
        new ControllerTypeSpecifierFull(package, $2->getShortName(), yylineno);
      $$ = new ControllerDefinition(AccessLevel::PUBLIC_ACCESS, 
                                    fullSpecifier, 
                                    std::get<0>(*$6), 
                                    *$4, 
                                    std::get<1>(*$6),
                                    $3,
                                    yylineno);
      delete $2;
      delete $4;
      delete $6;
    }
  ;

inner_controller_definition
  : access_specifier TCONTROLLER controller_type_specifier_short object_scope 
      implmeneted_interfaces '{' object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ControllerTypeSpecifierFull* fullSpecifier =
        new ControllerTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ControllerDefinition($1, 
                                    fullSpecifier, 
                                    std::get<0>(*$7), 
                                    *$5, 
                                    std::get<1>(*$7),
                                    $4,
                                    yylineno);
      delete $3;
      delete $5;
      delete $7;
    }
  ;

external_controller_definition
  : TEXTERNAL TCONTROLLER controller_type_specifier_full implmeneted_interfaces
      '{' external_object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ControllerTypeSpecifierFull* fullSpecifier =
        new ControllerTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ExternalControllerDefinition(fullSpecifier, 
                                            std::get<0>(*$6), 
                                            *$4,
                                            std::get<1>(*$6),
                                            yylineno);
      delete $3;
      delete $4;
      delete $6;
    }
  ;

object_scope
  : /* none */ { $$ = NULL; }
  | TINSCOPE object_type_specifier { $$ = $2; }
  ;

on_pool
  : /* none */ { $$ = 0; }
  | TONPOOL { $$ = 1; }
  ;

node_definition
  : TNODE node_type_specifier_top_level on_pool implmeneted_interfaces
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $2->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      NodeTypeSpecifierFull* fullSpecifier = 
        new NodeTypeSpecifierFull(package, $2->getShortName(), yylineno);
      $$ = new NodeDefinition(AccessLevel::PUBLIC_ACCESS, 
                              fullSpecifier, 
                              std::get<0>(*$6), 
                              *$4, 
                              std::get<1>(*$6),
                              $3,
                              yylineno);
      delete $2;
      delete $4;
      delete $6;
    }
  ;

inner_node_definition
  : access_specifier TNODE node_type_specifier_short on_pool implmeneted_interfaces
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      NodeTypeSpecifierFull* fullSpecifier =
        new NodeTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new NodeDefinition($1, 
                              fullSpecifier, 
                              std::get<0>(*$7), 
                              *$5, 
                              std::get<1>(*$7),
                              $4,
                              yylineno);
      delete $3;
      delete $5;
      delete $7;
    }
  ;

external_node_definition
  : TEXTERNAL TNODE node_type_specifier_full on_pool implmeneted_interfaces
      '{' external_object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      NodeTypeSpecifierFull* fullSpecifier =
        new NodeTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ExternalNodeDefinition(fullSpecifier,
                                      std::get<0>(*$7),
                                      *$5,
                                      std::get<1>(*$7),
                                      $4,
                                      yylineno);
      delete $3;
      delete $5;
      delete $7;
    }
  ;

model_definition
  : TMODEL model_type_specifier_top_level on_pool implmeneted_interfaces
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $2->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ModelTypeSpecifierFull* fullSpecifier =
        new ModelTypeSpecifierFull(package, $2->getShortName(), yylineno);
      $$ = new ModelDefinition(AccessLevel::PUBLIC_ACCESS,
                               fullSpecifier, 
                               std::get<0>(*$6), 
                               *$4, 
                               std::get<1>(*$6),
                               $3,
                               yylineno);
      delete $2;
      delete $4;
      delete $6;
    }
  ;

inner_model_definition
  : access_specifier TMODEL model_type_specifier_short on_pool implmeneted_interfaces
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ModelTypeSpecifierFull* fullSpecifier =
        new ModelTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ModelDefinition($1,
                               fullSpecifier, 
                               std::get<0>(*$7), 
                               *$5, 
                               std::get<1>(*$7),
                               $4,
                               yylineno);
      delete $3;
      delete $5;
      delete $7;
    }
  ;

external_model_definition
  : TEXTERNAL TMODEL model_type_specifier_full on_pool implmeneted_interfaces
      '{' external_object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ModelTypeSpecifierFull* fullSpecifier =
        new ModelTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ExternalModelDefinition(fullSpecifier,
                                       std::get<0>(*$7),
                                       *$5,
                                       std::get<1>(*$7),
                                       $4,
                                       yylineno);
      delete $3;
      delete $5;
      delete $7;
    }
  ;

external_method_definition
  : type_specifier TIDENTIFIER '(' method_definition_arguments ')' method_exceptions 
    method_qualifiers ';'
      {
        $$ = new ExternalMethodDefinition($1, *$2, *$4, *$6, $7, yylineno);
        delete $2;
        delete $4;
        delete $6;
      }
  ;

external_static_method_definition
  : TSTATIC type_specifier TIDENTIFIER '(' method_definition_arguments ')' method_exceptions ';'
      {
        $$ = new ExternalStaticMethodDefinition($2, *$3, *$5, *$7, yylineno);
        delete $3;
        delete $5;
        delete $7;
      }
  ;

implmeneted_interfaces
  : /* blank */ { $$ = new std::vector<IInterfaceTypeSpecifier*>(); }
  | TIMPLEMENTS interface_list { $$ = $2; }
  ;

extends_interfaces
  : /* blank */ { $$ = new std::vector<IInterfaceTypeSpecifier*>(); }
  | TEXTENDS interface_list { $$ = $2; }
  ;

interface_list
  : interface_type_specifier_top_level 
    { $$ = new std::vector<wisey::IInterfaceTypeSpecifier*>; $$->push_back($1); }
  | interface_type_specifier_inner
    { $$ = new std::vector<wisey::IInterfaceTypeSpecifier*>; $$->push_back($1); }
  | interface_list ',' interface_type_specifier_top_level { $$ = $1; $$->push_back($3); }
  | interface_list ',' interface_type_specifier_inner { $$ = $1; $$->push_back($3); }
  ;

object_element_declaration_list
  : /* blank */  
    { 
      $$ = new std::tuple<std::vector<IObjectElementDefinition*>,
                          std::vector<IObjectDefinition*>>();
    }
  | object_element_declaration_list object_element_declaration
    { std::get<0>(*$1).push_back($2); }
  | object_element_declaration_list inner_object_definition
    { std::get<1>(*$1).push_back($2); }
  ;

object_element_declaration
  : constant_definition
  | field_definition
  | method_definition
  | static_method_definition
  | llvm_function_definition
  ;

external_object_element_declaration_list
  : /* blank */
    { 
      $$ = new std::tuple<std::vector<IObjectElementDefinition*>,
                          std::vector<IObjectDefinition*>>();
    }
  | external_object_element_declaration_list external_object_element_declaration
    { std::get<0>(*$1).push_back($2); }
  | external_object_element_declaration_list external_object_definition
    { std::get<1>(*$1).push_back($2); }
  ;

external_object_element_declaration
  : constant_declaration
  | field_definition
  | external_method_definition
  | external_static_method_definition
  ;

constant_definition
  : access_specifier TCONSTANT type_specifier TCONSTANTIDENTIFIER '=' expression ';'
    { $$ = new ConstantDefinition($1, $3, *$4, $6, yylineno); delete $4; }
  ;

constant_declaration
  : TCONSTANT type_specifier TCONSTANTIDENTIFIER ';'
    { $$ = new ExternalConstantDeclaration($2, *$3, yylineno); delete $3; }
  ;

field_definition
  : type_specifier TFIELDIDENTIFIER ';'
    {
      $$ = ReceivedFieldDefinition::createImplied($1, *$2, yylineno);
      delete $2;
    }
  | TRECEIVE type_specifier TFIELDIDENTIFIER ';'
    {
      $$ = ReceivedFieldDefinition::create($2, *$3, yylineno);
      delete $3;
    }
  | TINJECT injectable_type_specifier TFIELDIDENTIFIER ';'
    {
      InjectionArgumentList arguments;
      $$ = InjectedFieldDefinition::createDelayed($2, *$3, arguments, yylineno);
      delete $3;
    }
  | TINJECT injectable_type_specifier TFIELDIDENTIFIER injection_argument_list ';'
    {
      $$ = InjectedFieldDefinition::createDelayed($2, *$3, *$4, yylineno);
      delete $3;
      delete $4;
    }
  | TINJECT TIMMEDIATE injectable_type_specifier TFIELDIDENTIFIER ';'
    {
      InjectionArgumentList arguments;
      $$ = InjectedFieldDefinition::createImmediate($3, *$4, arguments, yylineno);
      delete $4;
    }
  | TINJECT TIMMEDIATE injectable_type_specifier TFIELDIDENTIFIER injection_argument_list ';'
    {
      $$ = InjectedFieldDefinition::createImmediate($3, *$4, *$5, yylineno);
      delete $4;
      delete $5;
    }
  | TSTATE type_specifier TFIELDIDENTIFIER ';'
    {
      $$ = new StateFieldDefinition($2, *$3, yylineno);
      delete $3;
    }
  ;

compound_statement
  : '{' '}' { Block* block = new Block(); $$ = new CompoundStatement(block, yylineno); }
  | '{'  block_item_list '}' { $$ = new CompoundStatement($2, yylineno); }
  ;

block_item_list
  : block_item { $$ = new Block(); $$->getStatements().push_back($<statement>1); }
  | block_item_list block_item { $1->getStatements().push_back($<statement>2); }
  ;

block_item
  : declaration
  | statement
  ;

declaration
  : variable_declaration
  ;

statement
  : compound_statement { $$ = (IStatement*) $1; }
  | expression_statement
  | selection_statement
  | iteration_statement
  | print_statement
  | jump_statement
  | throw_statement
  | try_catch_statement
  | llvm_store
  ;

print_statement
  : TPRINTOUT '(' expression ')' ';'
    { $$ = new PrintOutStatement($3, yylineno); }
  | TPRINTERR '(' expression ')' ';'
    { $$ = new PrintErrStatement($3, yylineno); }
  | TPRINTFILE '(' expression ',' expression ')' ';'
    { $$ = new PrintFileStatement($3, $5, yylineno); }
  ;

case_statement_list_with_default
  : case_statement_list
  | case_statement_list default_case_statement { $$ = $1; $1->defaultStatement = $2; }
  ;

case_statement_list
  : case_statement { $$ = new SwitchCases(); $$->caseStatements.push_back($<case_statement>1); }
  | case_statement_list case_statement { $1->caseStatements.push_back($<case_statement>2); }
  ;

case_statement
  : TCASE expression ':' block_item_list
    { $$ = CaseStatement::newCaseStatement($2, $4, yylineno); }
  | TCASE expression ':' block_item_list TFALLTHROUGH ';'
    { $$ = CaseStatement::newCaseStatementWithFallThrough($2, $4, yylineno); }
  ;

default_case_statement
  : TDEFAULT ':' block_item_list { $$ = new DefaultCaseStatement($3); }
  ;

expression_statement
  : ';' { $$ = new EmptyStatement(); }
  | expression ';' { $$ = new ExpressionStatement($1, yylineno); }
  ;

selection_statement
  : TIF '(' expression ')' compound_statement TELSE statement
    { $$ = new IfElseStatement ($3, $5, $7); }
  | TIF '(' expression ')' compound_statement { $$ = new IfStatement($3, $5); }
  | TSWITCH '(' expression ')' '{' case_statement_list_with_default '}'
    { $$ = new SwitchStatement($3, $6); }
  ;

iteration_statement
  : TWHILE '(' expression ')' statement { $$ = new WhileStatement($3, $5); }
  | TDO statement TWHILE '(' expression ')' ';' { $$ = new DoStatement($2, $5); }
  | TFOR '(' expression_statement expression ';' ')' statement
    { $$ = ForStatement::newWithNoIncrement($3, $4, $7, yylineno); }
  | TFOR '(' expression_statement expression ';' expression ')' statement
    { $$ = new ForStatement($3, $4, $6, $8, yylineno); }
  | TFOR '(' declaration expression ';' ')' statement
    { $$ = ForStatement::newWithNoIncrement($3, $4, $7, yylineno); }
  | TFOR '(' declaration expression ';' expression ')' statement
    { $$ = new ForStatement($3, $4, $6, $8, yylineno); }
  ;

jump_statement
  : TCONTINUE ';' { $$ = new ContinueStatement(yylineno); }
  | TBREAK ';' { $$ = new BreakStatement(yylineno); }
  | TRETURN ';' { $$ =  new ReturnVoidStatement(yylineno); }
  | TRETURN expression ';' { $$ = new ReturnStatement($2, yylineno); }
  ;

throw_statement
  : TTHROW expression ';' { $$ = new ThrowStatement($2, yylineno); }
  ;

try_catch_statement
  : TTRY compound_statement catch_block { 
    $$ = new TryCatchStatement($2, *$3, yylineno); delete $3; 
  }
  ;

catch_block
  : catch_clause { $$ = new std::vector<Catch *>(); $$->push_back($1); }
  | catch_block catch_clause { $1->push_back($2); }
  ;

catch_clause
  : TCATCH '(' exception_type_specifier '*' TIDENTIFIER ')' compound_statement
    { $$ = new Catch($3, *$5, $7, yylineno); delete $5; }
  ;

variable_declaration
  : type_specifier identifier ';' { $$ = VariableDeclaration::create($1, $2, yylineno); }
  | type_specifier identifier '=' expression ';'
    { $$ = VariableDeclaration::createWithAssignment($1, $2, $4, yylineno); }
  ;

method_definition
  : access_specifier type_specifier TIDENTIFIER '(' method_definition_arguments ')'
      method_exceptions method_qualifiers compound_statement
    {
      $$ = new MethodDefinition($1, $2, *$3, *$5, *$7, $8, $9, yylineno);
      delete $3;
      delete $5;
      delete $7;
    }
  ;

static_method_definition
  : access_specifier TSTATIC type_specifier TIDENTIFIER '(' method_definition_arguments ')'
      method_exceptions compound_statement
    {
      $$ = new StaticMethodDefinition($1, $3, *$4, *$6, *$8, $9, yylineno);
      delete $4;
      delete $6;
      delete $8;
    }
  ;

interface_static_method_definition
  : access_specifier TSTATIC type_specifier TIDENTIFIER '(' method_definition_arguments ')'
      method_exceptions compound_statement
    {
      $$ = new StaticMethodDefinition($1, $3, *$4, *$6, *$8, $9, yylineno);
      delete $4;
      delete $6;
      delete $8;
    }
  ;

method_qualifiers
  : /* blank */ { $$ = new MethodQualifiers(yylineno); }
  | method_qualifier_set { $$ = $1; }
  ;

method_qualifier_set
  : method_qualifier
    { $$ = new MethodQualifiers(yylineno); $$->getMethodQualifierSet().insert($1); }
  | method_qualifier_set method_qualifier { $$ = $1; $$->getMethodQualifierSet().insert($2); }
  ;

method_qualifier
  : TOVERRIDE { $$ = MethodQualifier::OVERRIDE; }
  ;

method_exceptions
  : /* blank */ { $$ = new std::vector<IModelTypeSpecifier*>(); }
  | TTHROWS exception_type_specifier_list { $$ = $2; }
  ;

exception_type_specifier_list
  : exception_type_specifier { $$ = new std::vector<IModelTypeSpecifier*>(); $$->push_back($1); }
  | exception_type_specifier_list ',' exception_type_specifier { $1->push_back($3); }
  ;

method_argument_declaration
  : type_specifier identifier { $$ = VariableDeclaration::create($1, $2, yylineno); }
  ;

method_definition_arguments
  : /* blank */  { $$ = new VariableList(); }
  | method_argument_declaration
    { $$ = new VariableList(); $$->push_back($<variable_declaration>1); }
  | method_definition_arguments ',' method_argument_declaration
    { $1->push_back($<variable_declaration>3); }
  ;

access_specifier
  : TPUBLIC { $$ = AccessLevel::PUBLIC_ACCESS; }
  | TPRIVATE { $$ = AccessLevel::PRIVATE_ACCESS; }
  ;

constant_value
  : TCHAR { $$ = new CharConstant($1->substr(1, $1->length() - 2), yylineno); delete $1; }
  | TINTEGER { $$ = new IntConstant(atol($1->c_str()), yylineno); delete $1; }
  | TLONG { $$ = new LongConstant(atoll($1->c_str()), yylineno); delete $1; }
  | TFLOAT { $$ = new FloatConstant(atof($1->c_str()), yylineno); delete $1; }
  | TDOUBLE { $$ = new DoubleConstant(strtod($1->c_str(), NULL), yylineno); delete $1; }
  | TTRUE { $$ = new BooleanConstant(true, yylineno); }
  | TFALSE { $$ = new BooleanConstant(false, yylineno); }
  ;
    
string_literal
  : TSTRING_LITERAL 
    { $$ = new StringLiteral($1->substr(1, $1->length() - 2), yylineno); delete $1; }
  ;

constant_reference
  : object_type_specifier_dot TCONSTANTIDENTIFIER
    { $$ = new ConstantReference($1, *$2, yylineno); delete $2; }
  | TCONSTANTIDENTIFIER
    { $$ = new ConstantReference(NULL, *$1, yylineno); delete $1; }
  ;

static_method_call
  : object_type_specifier_dot TIDENTIFIER '(' call_arguments ')'
    {
      $$ = StaticMethodCall::create($1, *$2, *$4, yylineno);
      delete $2;
      delete $4;
    }
  ;

identifier
  : TIDENTIFIER { $$ = new Identifier(*$1, yylineno); delete $1; }
  ;

field_identifier
  : TFIELDIDENTIFIER { $$ = new Identifier(*$1, yylineno); delete $1; }
  ;

primary_expression
  : identifier { $$ = (IExpression*) $1; }
  | field_identifier
  | constant_value
  | constant_reference
  | string_literal
  | static_array_allocation
  | heap_builder
  | pool_builder
  | injection
  | static_method_call
  | llvm_function_call
  | llvm_reference
  | llvm_dereference
  | '(' expression ')' { $$ = $2; }
  | TNULL { $$ = new NullExpression(yylineno); }
  ;

postfix_expression
  : primary_expression
  | postfix_expression '.' TIDENTIFIER { $$ = new IdentifierChain($1, *$3, yylineno); delete $3; }
  | postfix_expression '[' expression ']' { $$ = new ArrayElementExpression($1, $3, yylineno); }
  | postfix_expression '(' call_arguments ')'
    { $$ = MethodCall::create($1, *$3, yylineno); delete $3; }
  | postfix_expression TINCOP { $$ = IncrementExpression::newIncrementByOne($1, yylineno); }
  | postfix_expression TDECOP { $$ = IncrementExpression::newDecrementByOne($1, yylineno); }
  ;

unary_expression
  : postfix_expression
  | '-' cast_expression { $$ = new NegateExpression($2, yylineno); }
  | '!' cast_expression { $$ = new BooleanNotExpression($2, yylineno); }
  ;

cast_expression
  : unary_expression
  | '(' type_specifier ')' cast_expression { $$ = new CastExpression($2, $4, yylineno); }
  ;

multiplicative_expression
  : cast_expression
  | multiplicative_expression '*' cast_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '*', $3, yylineno); }
  | multiplicative_expression '/' cast_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '/', $3, yylineno); }
  | multiplicative_expression '%' cast_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '%', $3, yylineno); }
  ;

additive_expression
  : multiplicative_expression
  | additive_expression '+' multiplicative_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '+', $3, yylineno); }
  | additive_expression '-' multiplicative_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '-', $3, yylineno); }
  ;

increment_by_expression
  : additive_expression
  | postfix_expression TINCBYOP additive_expression 
    { $$ = AdjustByExpression::newIncrementBy($1, $3, yylineno); }
  | postfix_expression TDECBYOP additive_expression 
    { $$ = AdjustByExpression::newDecrementBy($1, $3, yylineno); }
  ;

shift_expression
  : increment_by_expression
  | shift_expression TSHIFTLEFT additive_expression
    { $$ = new ShiftLeftExpression($1, $3, yylineno); }
  | shift_expression TSHIFTRIGHT additive_expression
    { $$ = new ShiftRightExpression($1, $3, yylineno); }
  ;

relational_expression
  : shift_expression
  | relational_expression '<' shift_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_LT, $3, yylineno); }
  | relational_expression '>' shift_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_GT, $3, yylineno); }
  | relational_expression TCLE shift_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_LE, $3, yylineno); }
  | relational_expression TCGE shift_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_GE, $3, yylineno); }
  ;

type_comparision_expression
  : relational_expression
  | type_comparision_expression TINSTANCEOF type_specifier
    { $$ = new TypeComparisionExpression($1, $3, yylineno); }
  ;

equality_expression
  : type_comparision_expression
  | equality_expression TCEQ relational_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_EQ, $3, yylineno); }
  | equality_expression TCNE relational_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_NE, $3, yylineno); }
  ;

bitwise_and_expression
  : equality_expression
  | bitwise_and_expression '&' equality_expression
    { $$ = new BitwiseAndExpression($1, $3, yylineno); }
  ;

logical_and_expression
  : bitwise_and_expression
  | logical_and_expression TANDOP bitwise_and_expression
    { $$ = new LogicalAndExpression($1, $3, yylineno); }
  ;

logical_or_expression
  : logical_and_expression
  | logical_or_expression TOROP logical_and_expression
    { $$ = new LogicalOrExpression($1, $3, yylineno); }
  ;

conditional_expression
  : logical_or_expression
  | logical_or_expression '?' expression ':' conditional_expression
    { $$ = new ConditionalExpression($1, $3, $5, yylineno); }
  ;

expression
  : conditional_expression
  | assignment
  | array_allocation
  ;

assignment
  : postfix_expression '=' expression { $$ = new Assignment($1, $3, yylineno); }
  ;

array_allocation
  : TNEW array_specific_type_specifier { $$ = new ArrayAllocation($2, yylineno); }
  ;

static_array_allocation
  : '{' static_array_elements '}' { $$ = new ArrayAllocationStatic(*$2, yylineno); delete $2; }
  ;

static_array_elements
  : expression { $$ = new ExpressionList(); $$->push_back($1); }
  | call_arguments ',' expression  { $1->push_back($3); $$ = $1; }
  | call_arguments ','  { $$ = $1; }
  ;

injection
  : TINJECT '(' object_type_specifier_injectable ')' injection_argument_list '.' TONHEAP '(' ')'
    { $$ = new Injector($3, *$5, yylineno); delete $5; }
  | TINJECT '(' object_type_specifier_injectable ')' '.' TONHEAP '(' ')'
    { InjectionArgumentList argumentList; $$ = new Injector($3, argumentList, yylineno); }
  ;

injection_argument_list
  : injection_argument { $$ = new InjectionArgumentList(); $$->push_back($1); }
  | injection_argument_list injection_argument { $1->push_back($2); }
  ;

injection_argument
  : '.' TIDENTIFIER '(' expression ')' { $$ = new InjectionArgument(*$2, $4); delete $2; }
  ;

heap_builder
  : TBUILD '(' object_type_specifier_buildable ')' builder_argument_list
    '.' TONHEAP '(' ')'
    { $$ = new HeapBuilder($3, *$5, yylineno); delete $5; }
  | TBUILD '(' object_type_specifier_buildable ')' '.' TONHEAP '(' ')'
    { BuilderArgumentList argumentList; $$ = new HeapBuilder($3, argumentList, yylineno); }
  ;

pool_builder
  : TBUILD '(' object_type_specifier_buildable ')' builder_argument_list
    '.' TONPOOL '(' expression ')'
    { $$ = new PoolBuilder($3, *$5, $9, yylineno); delete $5; }
  | TBUILD '(' object_type_specifier_buildable ')' '.' TONPOOL '(' expression ')'
    { 
      BuilderArgumentList argumentList; 
      $$ = new PoolBuilder($3, argumentList, $8, yylineno); 
    }
  ;


builder_argument_list
  : builder_argument { $$ = new BuilderArgumentList(); $$->push_back($1); }
  | builder_argument_list builder_argument { $1->push_back($2); }
  ;

builder_argument
  : '.' TIDENTIFIER '(' expression ')' { $$ = new BuilderArgument(*$2, $4); delete $2; }
  ;

call_arguments
  : /* blank */  { $$ = new ExpressionList(); }
  | call_argument { $$ = new ExpressionList(); $$->push_back($1); }
  | call_arguments ',' call_argument  { $1->push_back($3); }
  ;

call_argument
  : expression
  | object_type_specifier_dot TIDENTIFIER
    { $$ = new LLVMFunctionIdentifier($1, *$2, yylineno); delete $2; } 
  ;

type_specifier
  : non_array_type_specifier
  | array_type_specifier { $$ = (ITypeSpecifier*) $1; }
  | array_owner_type_specifier { $$ = (ITypeSpecifier*) $1; }
  | immutable_array_type_specifier { $$ = (ITypeSpecifier*) $1; }
  | immutable_array_owner_type_specifier { $$ = (ITypeSpecifier*) $1; }
  ;

// Compiler checks that injected type is not a primitive or a reference
injectable_type_specifier
  : non_array_type_specifier
  | array_specific_owner_type_specifier
  ;

non_array_type_specifier
  : primitive_type_specifier
  | object_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | object_owner_type_specifier
  | native_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  ;

primitive_type_specifier
  : TTYPEBOOLEAN { $$ = PrimitiveTypes::BOOLEAN->newTypeSpecifier(yylineno); }
  | TTYPECHAR { $$ = PrimitiveTypes::CHAR->newTypeSpecifier(yylineno); }
  | TTYPEDOUBLE { $$ = PrimitiveTypes::DOUBLE->newTypeSpecifier(yylineno); }
  | TTYPEFLOAT { $$ = PrimitiveTypes::FLOAT->newTypeSpecifier(yylineno); }
  | TTYPEINT { $$ = PrimitiveTypes::INT->newTypeSpecifier(yylineno); }
  | TTYPELONG { $$ = PrimitiveTypes::LONG->newTypeSpecifier(yylineno); }
  | TTYPESTRING { $$ = PrimitiveTypes::STRING->newTypeSpecifier(yylineno); }
  | TTYPEVOID { $$ = PrimitiveTypes::VOID->newTypeSpecifier(yylineno); }
  ;

immutable_array_type_specifier
  : TIMMUTABLE array_type_specifier { $$ = new ImmutableArrayTypeSpecifier($2); }
  ;

immutable_array_owner_type_specifier
  : immutable_array_type_specifier '*' { $$ = new ImmutableArrayOwnerTypeSpecifier($1); }
  ;

array_owner_type_specifier
  : array_type_specifier '*'
    { $$ = new ArrayOwnerTypeSpecifier($1); }
  ;

array_type_specifier
  : non_array_type_specifier array_undefined_dimensions_list
    { $$ = new ArrayTypeSpecifier($1, $2, yylineno); }
  ;

array_specific_owner_type_specifier
  : array_specific_type_specifier '*'
    { $$ = new ArraySpecificOwnerTypeSpecifier($1); }
  ;

array_specific_type_specifier
  : non_array_type_specifier array_dimensions
    { $$ = new ArraySpecificTypeSpecifier($1, *$2, yylineno); delete $2; }
  ;

array_dimensions
  : '[' expression ']' 
    { 
      $$ = new std::list<const IExpression*>(); 
      $$->push_back($2); 
    }
  | array_dimensions '[' expression ']' 
    { $1->push_back($3); $$ = $1; }
  ;

array_undefined_dimensions_list
  : '[' ']' { $$ = 1u; }
  | array_undefined_dimensions_list '[' ']' { $$ = $1 + 1; }

object_owner_type_specifier
  : object_type_specifier '*' { $$ = new ObjectOwnerTypeSpecifier($1); }
  ;

object_type_specifier_dot
  : object_type_specifier_short '.'
  | object_type_specifier_top_level_full '.'
  | object_type_specifier_inner_short '.'
  | object_type_specifier_inner_full '.'
  ;

object_type_specifier
  : object_type_specifier_short
  | object_type_specifier_top_level_full
  | object_type_specifier_inner_short
  | object_type_specifier_inner_full
  ;

object_type_specifier_inner_short
  : model_type_specifier_inner_short { $$ = (IObjectTypeSpecifier*) $1; }
  | controller_type_specifier_inner_short { $$ = (IObjectTypeSpecifier*) $1; }
  | interface_type_specifier_inner_short { $$ = (IObjectTypeSpecifier*) $1; }
  | node_type_specifier_inner_short { $$ = (IObjectTypeSpecifier*) $1; }
  ;

object_type_specifier_inner_full
  : model_type_specifier_inner_full { $$ = (IObjectTypeSpecifier*) $1; }
  | controller_type_specifier_inner_full { $$ = (IObjectTypeSpecifier*) $1; }
  | interface_type_specifier_inner_full { $$ = (IObjectTypeSpecifier*) $1; }
  | node_type_specifier_inner_full { $$ = (IObjectTypeSpecifier*) $1; }
  ;

object_type_specifier_short
  : model_type_specifier_short { $$ = $1; }
  | controller_type_specifier_short { $$ = $1; }
  | interface_type_specifier_short { $$ = $1; }
  | node_type_specifier_short { $$ = $1; }
  ;

object_type_specifier_top_level_full
  : model_type_specifier_top_level_full { $$ = $1; }
  | controller_type_specifier_top_level_full { $$ = $1; }
  | interface_type_specifier_top_level_full { $$ = $1; }
  | node_type_specifier_top_level_full { $$ = $1; }
  ;

model_type_specifier
  : model_type_specifier_top_level
  | model_type_specifier_inner
  ;

model_type_specifier_full
  : model_type_specifier_top_level_full
  | model_type_specifier_inner_full
  ;

model_type_specifier_top_level
  : model_type_specifier_short
  | model_type_specifier_top_level_full
  ;

model_type_specifier_short
  : TMODELIDENTIFIER
    { $$ = new ModelTypeSpecifier(NULL, *$1, yylineno); delete $1; }
  ;

model_type_specifier_top_level_full
  : postfix_expression '.' TMODELIDENTIFIER 
    { $$ = new ModelTypeSpecifierFull($1, *$3, yylineno); delete $3; }
  ;

model_type_specifier_inner
  : model_type_specifier_inner_short
  | model_type_specifier_inner_full
  ;

model_type_specifier_inner_short
  : object_type_specifier_short '.' TMODELIDENTIFIER 
    { 
      $$ = new ModelTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

model_type_specifier_inner_full
  : object_type_specifier_top_level_full '.' TMODELIDENTIFIER 
    { 
      $$ = new ModelTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

controller_type_specifier
  : controller_type_specifier_top_level
  | controller_type_specifier_inner
  ;

controller_type_specifier_full
  : controller_type_specifier_top_level_full
  | controller_type_specifier_inner_full
  ;

controller_type_specifier_top_level
  : controller_type_specifier_short
  | controller_type_specifier_top_level_full
  ;

controller_type_specifier_short
  : TCONTROLLERIDENTIFIER 
    { $$ = new ControllerTypeSpecifier(NULL, *$1, yylineno); delete $1; }
  ;  

controller_type_specifier_top_level_full
  : postfix_expression '.' TCONTROLLERIDENTIFIER 
    { $$ = new ControllerTypeSpecifierFull($1, *$3, yylineno); delete $3; }
  ;

controller_type_specifier_inner
  : controller_type_specifier_inner_short
  | controller_type_specifier_inner_full
  ;

controller_type_specifier_inner_short
  : object_type_specifier_short '.' TCONTROLLERIDENTIFIER 
    { 
      $$ = new ControllerTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

controller_type_specifier_inner_full
  : object_type_specifier_top_level_full '.' TCONTROLLERIDENTIFIER 
    { 
      $$ = new ControllerTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

interface_type_specifier
  : interface_type_specifier_top_level
  | interface_type_specifier_inner
  ;

interface_type_specifier_full
  : interface_type_specifier_top_level_full
  | interface_type_specifier_inner_full
  ;

interface_type_specifier_top_level
  : interface_type_specifier_short
  | interface_type_specifier_top_level_full
  ;

interface_type_specifier_short
  : TINTERFACEIDENTIFIER 
    { $$ = new InterfaceTypeSpecifier(NULL, *$1, yylineno); delete $1; }
  ;

interface_type_specifier_top_level_full
  : postfix_expression '.' TINTERFACEIDENTIFIER 
    { $$ = new InterfaceTypeSpecifierFull($1, *$3, yylineno); delete $3; }
  ;

interface_type_specifier_inner
  : interface_type_specifier_inner_short
  | interface_type_specifier_inner_full
  ;

interface_type_specifier_inner_short
  : object_type_specifier_short '.' TINTERFACEIDENTIFIER 
    { 
      $$ = new InterfaceTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

interface_type_specifier_inner_full
  : object_type_specifier_top_level_full '.' TINTERFACEIDENTIFIER 
    { 
      $$ = new InterfaceTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

node_type_specifier
  : node_type_specifier_top_level
  | node_type_specifier_inner
  ;

node_type_specifier_full
  : node_type_specifier_top_level_full
  | node_type_specifier_inner_full
  ;

node_type_specifier_top_level
  : node_type_specifier_short
  | node_type_specifier_top_level_full
  ;

node_type_specifier_short
  : TNODEIDENTIFIER
    { $$ = new NodeTypeSpecifier(NULL, *$1, yylineno); delete $1; }
  ;  

node_type_specifier_top_level_full
  : postfix_expression '.' TNODEIDENTIFIER 
    { $$ = new NodeTypeSpecifierFull($1, *$3, yylineno); delete $3; }
  ;

node_type_specifier_inner
  : node_type_specifier_inner_short
  | node_type_specifier_inner_full
  ;

node_type_specifier_inner_short
  : object_type_specifier_short '.' TNODEIDENTIFIER 
    { 
      $$ = new NodeTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

node_type_specifier_inner_full
  : object_type_specifier_top_level_full '.' TNODEIDENTIFIER 
    { 
      $$ = new NodeTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

exception_type_specifier
  : model_type_specifier { $$ = (ModelTypeSpecifier*) $1; }
  ;

object_type_specifier_buildable
  : model_type_specifier { $$ = (IBuildableObjectTypeSpecifier*) $1; }
  | node_type_specifier { $$ = (IBuildableObjectTypeSpecifier*) $1; }
  ;

object_type_specifier_injectable
  : interface_type_specifier { $$ = (IObjectTypeSpecifier*) $1; }
  | controller_type_specifier { $$ = (IObjectTypeSpecifier*) $1; }
  ;

%%


