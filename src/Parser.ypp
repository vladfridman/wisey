%{
  #include <sstream>
  
  #include "wisey/AccessLevel.hpp"
  #include "wisey/AdditiveMultiplicativeExpression.hpp"
  #include "wisey/ArrayAllocation.hpp"
  #include "wisey/ArrayAllocationStatic.hpp"
  #include "wisey/ArrayElementExpression.hpp"
  #include "wisey/ArrayOwnerTypeSpecifier.hpp"
  #include "wisey/ArraySpecificOwnerTypeSpecifier.hpp"
  #include "wisey/ArraySpecificTypeSpecifier.hpp"
  #include "wisey/ArrayTypeSpecifier.hpp"
  #include "wisey/Assignment.hpp"
  #include "wisey/BindAction.hpp"
  #include "wisey/BindActionGlobalStatement.hpp"
  #include "wisey/Block.hpp"
  #include "wisey/BooleanConstant.hpp"
  #include "wisey/BooleanNotExpression.hpp"
  #include "wisey/BreakStatement.hpp"
  #include "wisey/Catch.hpp"
  #include "wisey/CaseStatement.hpp"
  #include "wisey/CastExpression.hpp"
  #include "wisey/CharConstant.hpp"
  #include "wisey/CompoundStatement.hpp"
  #include "wisey/ConditionalExpression.hpp"
  #include "wisey/ConstantDefinition.hpp"
  #include "wisey/ConstantReference.hpp"
  #include "wisey/ContinueStatement.hpp"
  #include "wisey/ControllerDefinition.hpp"
  #include "wisey/ControllerTypeSpecifier.hpp"
  #include "wisey/ControllerTypeSpecifierFull.hpp"
  #include "wisey/DefaultCaseStatement.hpp"
  #include "wisey/DoStatement.hpp"
  #include "wisey/DoubleConstant.hpp"
  #include "wisey/EmptyExpression.hpp"
  #include "wisey/EmptyStatement.hpp"
  #include "wisey/ExitStatement.hpp"
  #include "wisey/ExpressionStatement.hpp"
  #include "wisey/ExternalControllerDefinition.hpp"
  #include "wisey/ExternalInterfaceDefinition.hpp"
  #include "wisey/ExternalMethodDefinition.hpp"
  #include "wisey/ExternalModelDefinition.hpp"
  #include "wisey/ExternalNodeDefinition.hpp"
  #include "wisey/ExternalThreadDefinition.hpp"
  #include "wisey/ExternalStaticMethodDefinition.hpp"
  #include "wisey/FakeExpression.hpp"
  #include "wisey/FixedFieldDefinition.hpp"
  #include "wisey/FloatConstant.hpp"
  #include "wisey/ForStatement.hpp"
  #include "wisey/Identifier.hpp"
  #include "wisey/IdentifierChain.hpp"
  #include "wisey/IdentifierReference.hpp"
  #include "wisey/IfElseStatement.hpp"
  #include "wisey/IfStatement.hpp"
  #include "wisey/IGlobalStatement.hpp"
  #include "wisey/ImportStatement.hpp"
  #include "wisey/IncrementExpression.hpp"
  #include "wisey/InjectedFieldDefinition.hpp"
  #include "wisey/InjectionArgument.hpp"
  #include "wisey/Injector.hpp"
  #include "wisey/IntConstant.hpp"
  #include "wisey/InterfaceDefinition.hpp"
  #include "wisey/InterfaceTypeSpecifier.hpp"
  #include "wisey/InterfaceTypeSpecifierFull.hpp"
  #include "wisey/IControllerTypeSpecifier.hpp"
  #include "wisey/IInterfaceTypeSpecifier.hpp"
  #include "wisey/ILLVMTypeSpecifier.hpp"
  #include "wisey/IModelTypeSpecifier.hpp"
  #include "wisey/INodeTypeSpecifier.hpp"
  #include "wisey/IObjectElementDefinition.hpp"
  #include "wisey/IObjectDefinition.hpp"
  #include "wisey/IObjectTypeSpecifier.hpp"
  #include "wisey/ITypeSpecifier.hpp"
  #include "wisey/LLVMArrayTypeSpecifer.hpp"
  #include "wisey/LLVMExternalStructDefinition.hpp"
  #include "wisey/LLVMGlobalDeclaration.hpp"
  #include "wisey/LLVMFunctionCall.hpp"
  #include "wisey/LLVMFunctionDeclaration.hpp"
  #include "wisey/LLVMFunctionDefinition.hpp"
  #include "wisey/LLVMFunctionTypeSpecifier.hpp"
  #include "wisey/LLVMPointerTypeSpecifier.hpp"
  #include "wisey/LLVMPrimitiveTypes.hpp"
  #include "wisey/LLVMStructDefinition.hpp"
  #include "wisey/LLVMStructSpecifier.hpp"
  #include "wisey/LLVMVariableDeclaration.hpp"
  #include "wisey/Log.hpp"
  #include "wisey/LogicalAndExpression.hpp"
  #include "wisey/LogicalOrExpression.hpp"
  #include "wisey/LongConstant.hpp"
  #include "wisey/MethodCall.hpp"
  #include "wisey/MethodDefinition.hpp"
  #include "wisey/MethodQualifier.hpp"
  #include "wisey/MethodSignatureDeclaration.hpp"
  #include "wisey/ModelDefinition.hpp"
  #include "wisey/ModelTypeSpecifier.hpp"
  #include "wisey/ModelTypeSpecifierFull.hpp"
  #include "wisey/NegateExpression.hpp"
  #include "wisey/NodeDefinition.hpp"
  #include "wisey/NodeTypeSpecifier.hpp"
  #include "wisey/NodeTypeSpecifierFull.hpp"
  #include "wisey/NullExpression.hpp"
  #include "wisey/ObjectBuilder.hpp"
  #include "wisey/ObjectBuilderArgument.hpp"
  #include "wisey/ObjectOwnerTypeSpecifier.hpp"
  #include "wisey/PackageType.hpp"
  #include "wisey/PrimitiveTypes.hpp"
  #include "wisey/PrimitiveTypeSpecifier.hpp"
  #include "wisey/PrintOutStatement.hpp"
  #include "wisey/PrintErrStatement.hpp"
  #include "wisey/ProgramFile.hpp"
  #include "wisey/ReceivedFieldDefinition.hpp"
  #include "wisey/RelationalExpression.hpp"
  #include "wisey/ReturnStatement.hpp"
  #include "wisey/ReturnVoidStatement.hpp"
  #include "wisey/StringLiteral.hpp"
  #include "wisey/StateFieldDefinition.hpp"
  #include "wisey/StaticMethodCall.hpp"
  #include "wisey/StaticMethodDefinition.hpp"
  #include "wisey/SwitchStatement.hpp"
  #include "wisey/ThreadExpression.hpp"
  #include "wisey/ThreadDefinition.hpp"
  #include "wisey/ThreadTypeSpecifier.hpp"
  #include "wisey/ThreadTypeSpecifierFull.hpp"
  #include "wisey/TryCatchStatement.hpp"
  #include "wisey/ThrowStatement.hpp"
  #include "wisey/TypeComparisionExpression.hpp"
  #include "wisey/VariableDeclaration.hpp"
  #include "wisey/WiseyModelOwnerTypeSpecifier.hpp"
  #include "wisey/WiseyModelTypeSpecifier.hpp"
  #include "wisey/WiseyObjectOwnerTypeSpecifier.hpp"
  #include "wisey/WiseyObjectTypeSpecifier.hpp"
  #include "wisey/WhileStatement.hpp"
  
  using namespace std;
  using namespace wisey;
  
  ProgramFile *programFile;
  PackageType* filePackageType;
  
  extern int yylex();
  extern int yylineno;
  extern std::string SourceFile;

  #define YYDEBUG 1

  void yyerror(const char *error_msg) {
    printf("%s(%d): %s\n", SourceFile.c_str(), yylineno, error_msg);
    exit(1);
  }
%}

%error-verbose
%locations

/** 
 * Data types used for parsing
 */
%union {
  wisey::IExpression* expression;
  wisey::IStatement* statement;
  wisey::IGlobalStatement* global_statement;
  wisey::GlobalStatementList* global_statement_list;
  wisey::Block* block;
  wisey::BindAction* bind_action;
  wisey::CompoundStatement* compound_statement;
  wisey::CaseStatement* case_statement;
  wisey::DefaultCaseStatement* default_case_statement;
  wisey::SwitchCases* switch_cases;
  const wisey::ITypeSpecifier* type_specifier;
  wisey::ArrayTypeSpecifier* array_type_specifier;
  wisey::ArrayOwnerTypeSpecifier* array_owner_type_specifier;
  wisey::ArraySpecificTypeSpecifier* array_specific_type_specifier;
  wisey::IObjectDefinition* object_definition;
  wisey::IObjectTypeSpecifier* object_type_specifier;
  wisey::Identifier* identifier;
  wisey::VariableDeclaration* variable_declaration;
  wisey::ObjectBuilderArgument* object_builder_argument;
  wisey::ObjectBuilderArgumentList* object_builder_argument_list;
  wisey::InjectionArgument* injection_argument;
  wisey::InjectionArgumentList* injection_argument_list;
  wisey::IModelTypeSpecifier* model_type_specifier;
  wisey::IControllerTypeSpecifier* controller_type_specifier;
  wisey::IInterfaceTypeSpecifier* interface_type_specifier;
  wisey::IObjectElementDefinition* object_element;
  wisey::INodeTypeSpecifier* node_type_specifier;
  wisey::IThreadTypeSpecifier* thread_type_specifier;
  wisey::AccessLevel access_specifier;
  wisey::MethodSignatureDeclaration* method_signature;
  wisey::Catch* catch_clause;
  wisey::ProgramFile* program_file;
  const wisey::ILLVMTypeSpecifier* llvm_type_specifier;
  wisey::LLVMPointerTypeSpecifier* llvm_pointer_type_specifier;
  wisey::LLVMVariableDeclaration* llvm_variable_declaration;
  wisey::MethodQualifier method_qualifier;
  std::vector<wisey::Catch*>* catch_vector;
  std::vector<wisey::VariableDeclaration*>* variable_vector;
  std::vector<const wisey::IExpression*>* expression_vector;
  std::vector<wisey::MethodSignatureDeclaration*>* method_signature_vector;
  std::tuple<std::vector<wisey::IObjectElementDefinition*>,
             std::vector<wisey::IObjectDefinition*>>* object_element_list;
  std::vector<wisey::ITypeSpecifier*>* type_specifier_list;
  std::vector<wisey::IModelTypeSpecifier*>* model_type_specifier_list;
  std::vector<wisey::IInterfaceTypeSpecifier*>* interface_list;
  std::list<const wisey::IExpression*>* list_of_const_expressions;
  std::list<unsigned long>* list_of_ulongs;
  std::vector<const wisey::ITypeSpecifier*>* const_type_specifier_list;
  std::vector<wisey::LLVMVariableDeclaration*>* llvm_variable_vector;
  wisey::MethodQualifierSet* method_qualifiers;
  std::string* string;
  unsigned long unsignedlong;
  int token;
}

/**
 * Tokens used in wisey 
 */
%token <string> TIDENTIFIER TCHAR TINTEGER TLONG TFLOAT TDOUBLE STRING_LITERAL TMODELIDENTIFIER
%token <string> TFIELDIDENTIFIER TINTERFACEIDENTIFIER TCONTROLLERIDENTIFIER TNODEIDENTIFIER
%token <string> TCONSTANTIDENTIFIER TTHREADIDENTIFIER

%token <token> TANDOP TBIND TBREAK TBUILD TBUILDER TCASE TCATCH TCEQ TCGE TCLE TINCOP
%token <token> TCONSTANT TCONTINUE TCNE TCONTROLLER TDECOP TDO TDOUBLECOLON TELSE TEXIT TEXTENDS 
%token <token> TEXTERNAL TDEFAULT TEXPOSED TFALLTHROUGH TFALSE TFIXED TFOR TIF TIMPLEMENTS TIMPORT
%token <token> TINJECTOR TINJECT TINSTANCEOF TINTERFACE  
%token <token> TLLVM TLLVMARRAY TLLVMFUNCTION TLLVMI1 TLLVMI8 TLLVMI16 TLLVMI32 TLLVMI64
%token <token> TLLVMPOINTER TLLVMSTRUCT TLLVMVOID TLLVMREFERENCE
%token <token> TMODEL TNEW TNODE TNULL TOROP TOVERRIDE TPACKAGE 
%token <token> TPRINTERR TPRINTOUT TPRIVATE TPUBLIC TRECEIVE TRETURN TSTATE TSTATIC 
%token <token> TSWITCH TTHREAD TTHROW TTHROWS TTO TTRY TTRUE TTYPEBOOLEAN 
%token <token> TTYPECHAR TTYPEDOUBLE TTYPEFLOAT TTYPEINT TTYPELONG TTYPESTRING TTYPEVOID TWHILE
%token <token> TWISEY TWISEYMODEL TWISEYOBJECT 

/**
 * Parsing tree node types
 */
%type <identifier> identifier
%type <string> package_optional package_name string_identifier llvm_function_name
%type <expression> string_literal constant_value expression multiplicative_expression 
%type <expression> additive_expression relational_expression equality_expression
%type <expression> logical_and_expression logical_or_expression
%type <expression> conditional_expression postfix_expression object_builder
%type <expression> cast_expression primary_expression assignment unary_expression
%type <expression> type_comparision_expression injection array_allocation static_array_allocation
%type <expression> thread_expression constant_reference static_method_call field_identifier
%type <expression> llvm_function_call llvm_reference
%type <variable_vector> method_definition_arguments
%type <expression_vector> call_arguments string_concatenation_list static_array_elements
%type <block> block_item_list 
%type <program_file> program
%type <statement> exit_statement statement variable_declaration
%type <statement> method_argument_declaration jump_statement block_item
%type <statement> declaration expression_statement iteration_statement
%type <statement> print_statement selection_statement 
%type <statement> throw_statement try_catch_statement
%type <statement> llvm_function_argument_declaration
%type <global_statement> bind_action_statement import_statement global_statement llvm_statement 
%type <global_statement> llvm_struct_definition llvm_function_declaration 
%type <global_statement> llvm_external_struct_definition llvm_global_declaration
%type <object_definition> controller_definition interface_definition model_definition
%type <object_definition> node_definition thread_definition external_thread_definition
%type <object_definition> object_definition external_object_definition external_model_definition
%type <object_definition> external_node_definition external_controller_definition
%type <object_definition> external_interface_definition
%type <object_definition> inner_controller_definition inner_interface_definition 
%type <object_definition> inner_model_definition inner_node_definition
%type <object_definition> inner_object_definition inner_thread_definition
%type <global_statement_list> global_statement_list
%type <compound_statement> compound_statement
%type <case_statement> case_statement
%type <default_case_statement> default_case_statement
%type <switch_cases> case_statement_list case_statement_list_with_default
%type <object_element> object_element_declaration external_object_element_declaration
%type <object_element> field_definition method_definition static_method_definition
%type <object_element> external_method_definition interface_static_method_definition
%type <object_element> constant_definition interface_element_declaration method_signature
%type <object_element> external_interface_element_declaration external_static_method_definition
%type <object_element> llvm_function_definition
%type <object_element_list> object_element_declaration_list 
%type <object_element_list> external_object_element_declaration_list
%type <object_element_list> interface_element_delcaration_list
%type <object_element_list> external_interface_element_delcaration_list
%type <array_type_specifier> array_type_specifier
%type <array_owner_type_specifier> array_owner_type_specifier
%type <array_specific_type_specifier> array_specific_type_specifier
%type <object_type_specifier> object_type_specifier
%type <object_type_specifier> object_type_specifier_dot
%type <object_type_specifier> object_type_specifier_short 
%type <object_type_specifier> object_type_specifier_buildable
%type <object_type_specifier> object_type_specifier_top_level_full
%type <object_type_specifier> object_type_specifier_injectable 
%type <object_type_specifier> object_type_specifier_inner_short
%type <object_type_specifier> object_type_specifier_inner_full
%type <object_builder_argument_list> object_builder_argument_list
%type <object_builder_argument> object_builder_argument
%type <injection_argument_list> injection_argument_list
%type <injection_argument> injection_argument
%type <bind_action> bind_action
%type <model_type_specifier> model_type_specifier
%type <model_type_specifier> model_type_specifier_top_level
%type <model_type_specifier> model_type_specifier_short
%type <model_type_specifier> exception_type_specifier 
%type <model_type_specifier> model_type_specifier_top_level_full 
%type <model_type_specifier> model_type_specifier_inner 
%type <model_type_specifier> model_type_specifier_inner_short
%type <model_type_specifier> model_type_specifier_inner_full
%type <model_type_specifier> model_type_specifier_full
%type <thread_type_specifier> thread_type_specifier
%type <thread_type_specifier> thread_type_specifier_top_level
%type <thread_type_specifier> thread_type_specifier_short
%type <thread_type_specifier> thread_type_specifier_top_level_full 
%type <thread_type_specifier> thread_type_specifier_inner 
%type <thread_type_specifier> thread_type_specifier_inner_short
%type <thread_type_specifier> thread_type_specifier_inner_full
%type <thread_type_specifier> thread_type_specifier_full
%type <controller_type_specifier> controller_type_specifier 
%type <controller_type_specifier> controller_type_specifier_top_level 
%type <controller_type_specifier> controller_type_specifier_short 
%type <controller_type_specifier> controller_type_specifier_top_level_full
%type <controller_type_specifier> controller_type_specifier_inner
%type <controller_type_specifier> controller_type_specifier_inner_short
%type <controller_type_specifier> controller_type_specifier_inner_full
%type <controller_type_specifier> controller_type_specifier_full
%type <interface_type_specifier> interface_type_specifier
%type <interface_type_specifier> interface_type_specifier_top_level
%type <interface_type_specifier> interface_type_specifier_short
%type <interface_type_specifier> interface_type_specifier_top_level_full
%type <interface_type_specifier> interface_type_specifier_inner
%type <interface_type_specifier> interface_type_specifier_inner_short
%type <interface_type_specifier> interface_type_specifier_inner_full
%type <interface_type_specifier> interface_type_specifier_full
%type <node_type_specifier> node_type_specifier
%type <node_type_specifier> node_type_specifier_top_level
%type <node_type_specifier> node_type_specifier_short
%type <node_type_specifier> node_type_specifier_top_level_full
%type <node_type_specifier> node_type_specifier_inner
%type <node_type_specifier> node_type_specifier_inner_short
%type <node_type_specifier> node_type_specifier_inner_full
%type <node_type_specifier> node_type_specifier_full
%type <access_specifier> access_specifier
%type <catch_clause> catch_clause
%type <catch_vector> catch_block
%type <model_type_specifier_list> method_exceptions exception_type_specifier_list
%type <interface_list> interface_list implmeneted_interfaces extends_interfaces
%type <list_of_const_expressions> array_dimensions
%type <unsignedlong> array_undefined_dimensions_list
%type <type_specifier> type_specifier primitive_type_specifier object_owner_type_specifier 
%type <type_specifier> non_array_type_specifier injectable_type_specifier 
%type <type_specifier> array_specific_owner_type_specifier
%type <type_specifier> wisey_object_type_specifier wisey_object_owner_type_specifier
%type <type_specifier> wisey_model_type_specifier wisey_model_owner_type_specifier
%type <type_specifier> native_type_specifier llvm_type_specifier wisey_type_specifier
%type <type_specifier> llvm_non_array_type_specifier 
%type <type_specifier> llvm_void_type_specifier
%type <llvm_type_specifier> llvm_i_type_specifier llvm_struct_specifier llvm_array_specifier
%type <llvm_type_specifier> llvm_pointer_base_type_specifier llvm_function_type_specifier
%type <llvm_pointer_type_specifier> llvm_pointer_type_specifier
%type <const_type_specifier_list> native_type_specifier_list 
%type <const_type_specifier_list> llvm_function_argument_type_specifier_list
%type <llvm_variable_vector> llvm_function_definition_arguments
%type <list_of_ulongs> llvm_array_dimensions
%type <method_qualifier> method_qualifier
%type <method_qualifiers> method_qualifiers method_qualifier_set

%left '+' '-'
%left '*' '/' '%'

/**
 * Expect one shift/reduce conflit: the dangling ELSE
 */
%expect 0

/**
 * Top-most parsing tree node
 */
%start program

%%

program
  : package_optional global_statement_list
    { programFile = new ProgramFile(*$1, *$2); delete $1; delete $2; }
  ;

package_optional
  : /* blank */ { filePackageType = NULL; $$ = new std::string(""); }
  | TPACKAGE package_name ';' {  filePackageType = new PackageType(*$2); $$ = $2; }
  ;

package_name
  : TIDENTIFIER
  | package_name '.' TIDENTIFIER
    { $$ = $1; $$->append(std::string(".")); $$->append(*$3); delete $3; }
  ;

global_statement_list
  : global_statement { $$ = new GlobalStatementList(); $$->push_back($1); }
  | global_statement_list global_statement { $1->push_back($2); }
  ;

global_statement
  : object_definition { $$ = (IGlobalStatement*) $1; }
  | external_object_definition { $$ = (IGlobalStatement*) $1; }
  | bind_action_statement
  | import_statement
  | llvm_statement
  ;

llvm_statement
  : llvm_struct_definition
  | llvm_function_declaration
  | llvm_global_declaration
  | llvm_external_struct_definition
  ;

llvm_global_declaration
  : llvm_type_specifier TIDENTIFIER ';'
    { $$ = new LLVMGlobalDeclaration($1, *$2); delete $2; }
  ;

llvm_function_declaration
  : TLLVM TLLVMFUNCTION llvm_type_specifier TIDENTIFIER
    '(' llvm_function_argument_type_specifier_list ')' ';'
      { $$ = LLVMFunctionDeclaration::createInternal(*$4, $3, *$6); delete $4; delete $6; }
  | TEXTERNAL TLLVM TLLVMFUNCTION llvm_type_specifier TIDENTIFIER
    '(' llvm_function_argument_type_specifier_list ')' ';'
      { $$ = LLVMFunctionDeclaration::createExternal(*$5, $4, *$7); delete $5; delete $7; }
  ;

llvm_struct_definition
  : TLLVM TLLVMSTRUCT TIDENTIFIER '{' native_type_specifier_list '}' 
    { $$ = new LLVMStructDefinition(*$3, *$5, yylineno); delete $3; delete $5; }
  ;

llvm_external_struct_definition
  : TEXTERNAL TLLVM TLLVMSTRUCT TIDENTIFIER '{' native_type_specifier_list '}' 
    { $$ = new LLVMExternalStructDefinition(*$4, *$6, yylineno); delete $4; delete $6; }
  ;

native_type_specifier_list
  : native_type_specifier
    { $$ = new std::vector<const ITypeSpecifier*>(); $$->push_back($1); }
  | native_type_specifier_list ',' native_type_specifier
    { $1->push_back($3); $$ = $1; }
  | native_type_specifier_list ','
    { $$ = $1; }
  ;

native_type_specifier
  : llvm_type_specifier
  | wisey_type_specifier
  ;

llvm_type_specifier
  : llvm_non_array_type_specifier
  | llvm_array_specifier { $$ = (const ITypeSpecifier*) $1; }
  | llvm_pointer_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  ;

wisey_type_specifier
  : wisey_object_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | wisey_object_owner_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | wisey_model_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | wisey_model_owner_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  ; 

wisey_object_type_specifier
  : TWISEY TWISEYOBJECT { $$ = new WiseyObjectTypeSpecifier(); }
  ;

wisey_object_owner_type_specifier
  : wisey_object_type_specifier '*'
    { $$ = new WiseyObjectOwnerTypeSpecifier(); }
  ;

wisey_model_type_specifier
  : TWISEY TWISEYMODEL { $$ = new WiseyModelTypeSpecifier(); }
  ;

wisey_model_owner_type_specifier
  : wisey_model_type_specifier '*'
    { $$ = new WiseyModelOwnerTypeSpecifier(); }
  ;

llvm_non_array_type_specifier
  : llvm_i_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | llvm_struct_specifier { $$ = (const ITypeSpecifier*) $1; }
  | llvm_function_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | llvm_void_type_specifier
  ;

llvm_pointer_type_specifier
  : llvm_pointer_base_type_specifier TLLVMPOINTER
    { $$ = new LLVMPointerTypeSpecifier($1); }
  | llvm_pointer_type_specifier TLLVMPOINTER
    { $$ = new LLVMPointerTypeSpecifier($1); }
  ;

llvm_pointer_base_type_specifier
  : llvm_i_type_specifier
  | llvm_struct_specifier
  | llvm_function_type_specifier
  | llvm_array_specifier
  ;

llvm_i_type_specifier
  : TLLVM TLLVMI1 { $$ = LLVMPrimitiveTypes::I1->newTypeSpecifier(); }
  | TLLVM TLLVMI8 { $$ = LLVMPrimitiveTypes::I8->newTypeSpecifier(); }
  | TLLVM TLLVMI16 { $$ = LLVMPrimitiveTypes::I16->newTypeSpecifier(); }
  | TLLVM TLLVMI32 { $$ = LLVMPrimitiveTypes::I32->newTypeSpecifier(); }
  | TLLVM TLLVMI64 { $$ = LLVMPrimitiveTypes::I64->newTypeSpecifier(); }
  ;

llvm_void_type_specifier
  : TLLVM TLLVMVOID { $$ = LLVMPrimitiveTypes::VOID->newTypeSpecifier(); }

llvm_struct_specifier
  : TLLVM TLLVMSTRUCT TDOUBLECOLON TIDENTIFIER
    { $$ = new LLVMStructSpecifier(*$4, yylineno); delete $4; }
  ;

llvm_array_specifier
  : TLLVM TLLVMARRAY '(' llvm_non_array_type_specifier ',' llvm_array_dimensions ')'
    { $$ = new LLVMArrayTypeSpecifer($4, *$6); delete $6; }
  ;

llvm_array_dimensions
  : TINTEGER
    { 
      $$ = new std::list<unsigned long>();
      $$->push_back(atol($1->c_str()));
      delete $1;
    }
  | llvm_array_dimensions ',' TINTEGER 
    { $1->push_back(atol($3->c_str())); delete $3; $$ = $1; }
  ;

llvm_function_type_specifier
  : native_type_specifier '(' llvm_function_argument_type_specifier_list ')'
    { $$ = new LLVMFunctionTypeSpecifier($1, *$3); delete $3; }
  ;

llvm_function_argument_type_specifier_list
  : /* blank */ 
    { $$ = new std::vector<const ITypeSpecifier*>(); }
  | native_type_specifier
    { $$ = new std::vector<const ITypeSpecifier*>(); $$->push_back($1); }
  | llvm_function_argument_type_specifier_list ',' native_type_specifier
    { $1->push_back($3); $$ = $1; }
  ;

llvm_function_definition
  : access_specifier TLLVM TLLVMFUNCTION native_type_specifier TIDENTIFIER
    '(' llvm_function_definition_arguments ')' compound_statement
      { $$ = new LLVMFunctionDefinition(*$5, $1, $4, *$7, $9, yylineno); delete $5; delete $7; }
  ;

llvm_function_definition_arguments
  : /* blank */  { $$ = new LLVMVariableList(); }
  | llvm_function_argument_declaration
    { $$ = new LLVMVariableList(); $$->push_back($<llvm_variable_declaration>1); }
  | llvm_function_definition_arguments ',' llvm_function_argument_declaration
    { $$ = $1; $$->push_back($<llvm_variable_declaration>3); }
  ;

llvm_function_argument_declaration
  : native_type_specifier identifier { $$ = LLVMVariableDeclaration::create($1, $2, yylineno); }
  ;

llvm_function_call
  : TLLVM TLLVMFUNCTION TDOUBLECOLON llvm_function_name '(' call_arguments ')'
    {
      $$ = new LLVMFunctionCall(*$4, *$6);
      delete $4;
      delete $6;
    }
  ;

llvm_function_name
  : string_identifier { $$ = $1; }
  | llvm_function_name '.' string_identifier 
    { $$ = new std::string(*$1 + "." + *$3); delete $1; delete $3; }
  ;

llvm_reference
  : TLLVM TLLVMREFERENCE '(' TIDENTIFIER ')' 
    { $$ = new IdentifierReference(*$4); delete $4; }
  | TLLVM TLLVMREFERENCE '(' TFIELDIDENTIFIER ')' 
    { $$ = new IdentifierReference(*$4); delete $4; }
  ;

string_identifier
  : TIDENTIFIER
  | TMODELIDENTIFIER
  | TINTERFACEIDENTIFIER
  | TCONTROLLERIDENTIFIER
  | TNODEIDENTIFIER
  | TTHREADIDENTIFIER
  | TFIELDIDENTIFIER
  ;  

inner_object_definition
  : inner_controller_definition
  | inner_interface_definition
  | inner_model_definition
  | inner_node_definition
  | inner_thread_definition
  ;

object_definition
  : controller_definition
  | interface_definition
  | model_definition
  | node_definition
  | thread_definition
  ;

external_object_definition
  : external_controller_definition
  | external_interface_definition
  | external_model_definition
  | external_node_definition
  | external_thread_definition
  ;

import_statement
  : TIMPORT interface_type_specifier_full ';' { $$ = new ImportStatement($2); }
  | TIMPORT model_type_specifier_full ';' { $$ = new ImportStatement($2); }
  | TIMPORT controller_type_specifier_full ';' { $$ = new ImportStatement($2); }
  | TIMPORT node_type_specifier_full ';' { $$ = new ImportStatement($2); }
  | TIMPORT thread_type_specifier_full ';' { $$ = new ImportStatement($2); }
  ;

bind_action_statement
  : bind_action ';' { $$ = new BindActionGlobalStatement($1); }
  ;

bind_action
  : TBIND '(' interface_type_specifier ')' '.' TTO '(' controller_type_specifier ')'
    { 
      InjectionArgumentList injectionArgumentList;
      $$ = new BindAction($3, $8, injectionArgumentList); 
    }
  | TBIND '(' interface_type_specifier ')' '.' TTO '(' controller_type_specifier ')' 
      injection_argument_list
    { $$ = new BindAction($3, $8, *$10); delete $10; }
  ;

interface_definition
  : TINTERFACE interface_type_specifier_top_level extends_interfaces 
      '{' interface_element_delcaration_list '}'
    {
      IExpression* package = $2->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      InterfaceTypeSpecifierFull* fullSpecifier = 
        new InterfaceTypeSpecifierFull(package, $2->getShortName(), yylineno);
      $$ = new InterfaceDefinition(AccessLevel::PUBLIC_ACCESS,
                                   fullSpecifier, 
                                   *$3, 
                                   std::get<0>(*$5), 
                                   std::get<1>(*$5),
                                   yylineno);
      delete $2;
      delete $3;
      delete $5;
    }
  ;

inner_interface_definition
  : access_specifier TINTERFACE interface_type_specifier_short extends_interfaces 
      '{' interface_element_delcaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      InterfaceTypeSpecifierFull* fullSpecifier =
        new InterfaceTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new InterfaceDefinition($1,
                                   fullSpecifier, 
                                   *$4, 
                                   std::get<0>(*$6), 
                                   std::get<1>(*$6),
                                   yylineno);
      delete $3;
      delete $4;
      delete $6;
    }
  ;

external_interface_definition
  : TEXTERNAL TINTERFACE interface_type_specifier_top_level extends_interfaces 
      '{' external_interface_element_delcaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      InterfaceTypeSpecifierFull* fullSpecifier =
        new InterfaceTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ExternalInterfaceDefinition(fullSpecifier,
                                           *$4,
                                           std::get<0>(*$6),
                                           std::get<1>(*$6),
                                           yylineno);
      delete $3;
      delete $4;
      delete $6;
    }
  ;

external_interface_element_delcaration_list
  : /* blank */ 
    { 
      $$ = new std::tuple<std::vector<IObjectElementDefinition*>,
                          std::vector<IObjectDefinition*>>();
    }
  | external_interface_element_delcaration_list external_interface_element_declaration
    { std::get<0>(*$1).push_back($2); }
  | external_interface_element_delcaration_list external_object_definition
    { std::get<1>(*$1).push_back($2); }
  ;

external_interface_element_declaration
  : method_signature
  | constant_definition
  | external_static_method_definition
  ;

interface_element_delcaration_list
  : /* blank */ 
    { 
      $$ = new std::tuple<std::vector<IObjectElementDefinition*>,
                          std::vector<IObjectDefinition*>>();
    }
  | interface_element_delcaration_list interface_element_declaration
    { std::get<0>(*$1).push_back($2); }
  | interface_element_delcaration_list inner_object_definition
    { std::get<1>(*$1).push_back($2); }
  ;

interface_element_declaration
  : method_signature
  | constant_definition
  | interface_static_method_definition
  ;

method_signature
  : type_specifier TIDENTIFIER '(' method_definition_arguments ')' method_exceptions 
    method_qualifiers ';'
      {
        $$ = new MethodSignatureDeclaration($1, *$2, *$4, *$6, *$7, yylineno);
        delete $2;
        delete $4;
        delete $6;
        delete $7;
      }
  ;

controller_definition
  : TCONTROLLER controller_type_specifier_top_level implmeneted_interfaces 
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $2->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ControllerTypeSpecifierFull* fullSpecifier =
        new ControllerTypeSpecifierFull(package, $2->getShortName(), yylineno);
      $$ = new ControllerDefinition(AccessLevel::PUBLIC_ACCESS, 
                                    fullSpecifier, 
                                    std::get<0>(*$5), 
                                    *$3, 
                                    std::get<1>(*$5),
                                    yylineno);
      delete $2;
      delete $5;
      delete $3;
    }
  ;

inner_controller_definition
  : access_specifier TCONTROLLER controller_type_specifier_short implmeneted_interfaces 
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ControllerTypeSpecifierFull* fullSpecifier =
        new ControllerTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ControllerDefinition($1, 
                                    fullSpecifier, 
                                    std::get<0>(*$6), 
                                    *$4, 
                                    std::get<1>(*$6),
                                    yylineno);
      delete $3;
      delete $6;
      delete $4;
    }
  ;

external_controller_definition
  : TEXTERNAL TCONTROLLER controller_type_specifier_top_level_full implmeneted_interfaces
      '{' external_object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ControllerTypeSpecifierFull* fullSpecifier =
        new ControllerTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ExternalControllerDefinition(fullSpecifier, 
                                            std::get<0>(*$6), 
                                            *$4,
                                            std::get<1>(*$6),
                                            yylineno);
      delete $3;
      delete $6;
      delete $4;
    }
  ;

node_definition
  : TNODE node_type_specifier_top_level implmeneted_interfaces
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $2->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      NodeTypeSpecifierFull* fullSpecifier = 
        new NodeTypeSpecifierFull(package, $2->getShortName(), yylineno);
      $$ = new NodeDefinition(AccessLevel::PUBLIC_ACCESS, 
                              fullSpecifier, 
                              std::get<0>(*$5), 
                              *$3, 
                              std::get<1>(*$5),
                              yylineno);
      delete $2;
      delete $5;
      delete $3;
    }
  ;

inner_node_definition
  : access_specifier TNODE node_type_specifier_short implmeneted_interfaces
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      NodeTypeSpecifierFull* fullSpecifier =
        new NodeTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new NodeDefinition($1, 
                              fullSpecifier, 
                              std::get<0>(*$6), 
                              *$4, 
                              std::get<1>(*$6),
                              yylineno);
      delete $3;
      delete $6;
      delete $4;
    }
  ;

external_node_definition
  : TEXTERNAL TNODE node_type_specifier_top_level implmeneted_interfaces
      '{' external_object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      NodeTypeSpecifierFull* fullSpecifier =
        new NodeTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ExternalNodeDefinition(fullSpecifier,
                                      std::get<0>(*$6),
                                      *$4,
                                      std::get<1>(*$6),
                                      yylineno);
      delete $3;
      delete $6;
      delete $4;
    }
  ;

model_definition
  : TMODEL model_type_specifier_top_level implmeneted_interfaces
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $2->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ModelTypeSpecifierFull* fullSpecifier =
        new ModelTypeSpecifierFull(package, $2->getShortName(), yylineno);
      $$ = new ModelDefinition(AccessLevel::PUBLIC_ACCESS,
                               fullSpecifier, 
                               std::get<0>(*$5), 
                               *$3, 
                               std::get<1>(*$5),
                               yylineno);
      delete $2;
      delete $5;
      delete $3;
    }
  ;

inner_model_definition
  : access_specifier TMODEL model_type_specifier_short implmeneted_interfaces
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ModelTypeSpecifierFull* fullSpecifier =
        new ModelTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ModelDefinition($1,
                               fullSpecifier, 
                               std::get<0>(*$6), 
                               *$4, 
                               std::get<1>(*$6),
                               yylineno);
      delete $3;
      delete $6;
      delete $4;
    }
  ;

external_model_definition
  : TEXTERNAL TMODEL model_type_specifier_top_level_full implmeneted_interfaces
      '{' external_object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ModelTypeSpecifierFull* fullSpecifier =
        new ModelTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ExternalModelDefinition(fullSpecifier,
                                       std::get<0>(*$6),
                                       *$4,
                                       std::get<1>(*$6),
                                       yylineno);
      delete $3;
      delete $6;
      delete $4;
    }
  ;

thread_definition
  : TTHREAD thread_type_specifier_top_level implmeneted_interfaces 
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $2->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ThreadTypeSpecifierFull* fullSpecifier =
        new ThreadTypeSpecifierFull(package, $2->getShortName(), yylineno);
      $$ = new ThreadDefinition(AccessLevel::PUBLIC_ACCESS, 
                                fullSpecifier, 
                                std::get<0>(*$5), 
                                *$3, 
                                std::get<1>(*$5),
                                yylineno);
      delete $2;
      delete $3;
      delete $5;
    }
  ;

inner_thread_definition
  : access_specifier TTHREAD thread_type_specifier_short implmeneted_interfaces 
      '{' object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ThreadTypeSpecifierFull* fullSpecifier =
        new ThreadTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ThreadDefinition($1, 
                                fullSpecifier, 
                                std::get<0>(*$6), 
                                *$4, 
                                std::get<1>(*$6),
                                yylineno);
      delete $3;
      delete $6;
      delete $4;
    }
  ;

external_thread_definition
  : TEXTERNAL TTHREAD thread_type_specifier_top_level implmeneted_interfaces
      '{' external_object_element_declaration_list '}'
    {
      IExpression* package = $3->takePackage();
      package = package ? package : new FakeExpression(NULL, filePackageType);
      ThreadTypeSpecifierFull* fullSpecifier =
        new ThreadTypeSpecifierFull(package, $3->getShortName(), yylineno);
      $$ = new ExternalThreadDefinition(fullSpecifier, 
                                        std::get<0>(*$6), 
                                        *$4,
                                        std::get<1>(*$6),
                                        yylineno);
      delete $3;
      delete $6;
      delete $4;
    }
  ;

external_method_definition
  : type_specifier TIDENTIFIER '(' method_definition_arguments ')' method_exceptions 
    method_qualifiers ';'
      {
        $$ = new ExternalMethodDefinition($1, *$2, *$4, *$6, *$7, yylineno);
        delete $2;
        delete $4;
        delete $6;
        delete $7;
      }
  ;

external_static_method_definition
  : TSTATIC type_specifier TIDENTIFIER '(' method_definition_arguments ')' method_exceptions ';'
      {
        $$ = new ExternalStaticMethodDefinition($2, *$3, *$5, *$7, yylineno);
        delete $3;
        delete $5;
        delete $7;
      }
  ;

implmeneted_interfaces
  : /* blank */ { $$ = new std::vector<IInterfaceTypeSpecifier*>(); }
  | TIMPLEMENTS interface_list { $$ = $2; }
  ;

extends_interfaces
  : /* blank */ { $$ = new std::vector<IInterfaceTypeSpecifier*>(); }
  | TEXTENDS interface_list { $$ = $2; }
  ;

interface_list
  : interface_type_specifier_top_level 
    { $$ = new std::vector<wisey::IInterfaceTypeSpecifier*>; $$->push_back($1); }
  | interface_type_specifier_inner
    { $$ = new std::vector<wisey::IInterfaceTypeSpecifier*>; $$->push_back($1); }
  | interface_list ',' interface_type_specifier_top_level { $$ = $1; $$->push_back($3); }
  | interface_list ',' interface_type_specifier_inner { $$ = $1; $$->push_back($3); }
  ;

object_element_declaration_list
  : /* blank */  
    { 
      $$ = new std::tuple<std::vector<IObjectElementDefinition*>,
                          std::vector<IObjectDefinition*>>();
    }
  | object_element_declaration_list object_element_declaration
    { std::get<0>(*$1).push_back($2); }
  | object_element_declaration_list inner_object_definition
    { std::get<1>(*$1).push_back($2); }
  ;

object_element_declaration
  : constant_definition
  | field_definition
  | method_definition
  | static_method_definition
  | llvm_function_definition
  ;

external_object_element_declaration_list
  : /* blank */
    { 
      $$ = new std::tuple<std::vector<IObjectElementDefinition*>,
                          std::vector<IObjectDefinition*>>();
    }
  | external_object_element_declaration_list external_object_element_declaration
    { std::get<0>(*$1).push_back($2); }
  | external_object_element_declaration_list external_object_definition
    { std::get<1>(*$1).push_back($2); }
  ;

external_object_element_declaration
  : constant_definition
  | field_definition
  | external_method_definition
  | external_static_method_definition
  ;

constant_definition
  : access_specifier TCONSTANT type_specifier TCONSTANTIDENTIFIER '=' expression ';'
    { $$ = new ConstantDefinition($1, $3, *$4, $6); delete $4; }
  ;

field_definition
  : type_specifier TFIELDIDENTIFIER ';'
    {
      $$ = new FixedFieldDefinition($1, *$2);
      delete $2;
    }
  | TFIXED type_specifier TFIELDIDENTIFIER ';'
    {
      $$ = new FixedFieldDefinition($2, *$3);
      delete $3;
    }
  | TINJECT injectable_type_specifier TFIELDIDENTIFIER ';'
    {
      InjectionArgumentList arguments;
      $$ = new InjectedFieldDefinition($2, *$3, arguments);
      delete $3;
    }
  | TINJECT injectable_type_specifier TFIELDIDENTIFIER injection_argument_list ';'
    {
      $$ = new InjectedFieldDefinition($2, *$3, *$4);
      delete $3;
      delete $4;
    }
  | TRECEIVE type_specifier TFIELDIDENTIFIER ';'
    {
      $$ = new ReceivedFieldDefinition($2, *$3);
      delete $3;
    }
  | TSTATE type_specifier TFIELDIDENTIFIER ';'
    {
      $$ = new StateFieldDefinition($2, *$3);
      delete $3;
    }
  ;

compound_statement
  : '{' '}' { Block* block = new Block(); $$ = new CompoundStatement(block, yylineno); }
  | '{'  block_item_list '}' { $$ = new CompoundStatement($2, yylineno); }
  ;

block_item_list
  : block_item { $$ = new Block(); $$->getStatements().push_back($<statement>1); }
  | block_item_list block_item { $1->getStatements().push_back($<statement>2); }
  ;

block_item
  : declaration
  | statement
  ;

declaration
  : variable_declaration
  ;

statement
  : compound_statement { $$ = (IStatement*) $1; }
  | expression_statement
  | selection_statement
  | iteration_statement
  | print_statement
  | jump_statement
  | throw_statement
  | try_catch_statement
  | exit_statement
  ;

print_statement
  : TPRINTOUT '(' string_concatenation_list ')' ';' { $$ = new PrintOutStatement(*$3); delete $3; }
  | TPRINTERR '(' string_concatenation_list ')' ';' { $$ = new PrintErrStatement(*$3); delete $3; }
  ;

string_concatenation_list
  : cast_expression { $$ = new std::vector<const IExpression*>(); $$->push_back($1); }
  | string_concatenation_list '+' cast_expression { $1->push_back($3); $$ = $1; }
  ;

case_statement_list_with_default
  : case_statement_list
  | case_statement_list default_case_statement { $$ = $1; $1->defaultStatement = $2; }
  ;

case_statement_list
  : case_statement { $$ = new SwitchCases(); $$->caseStatements.push_back($<case_statement>1); }
  | case_statement_list case_statement { $1->caseStatements.push_back($<case_statement>2); }
  ;

case_statement
  : TCASE expression ':' block_item_list
    { $$ = CaseStatement::newCaseStatement($2, $4); }
  | TCASE expression ':' block_item_list TFALLTHROUGH ';'
    { $$ = CaseStatement::newCaseStatementWithFallThrough($2, $4); }
  ;

default_case_statement
  : TDEFAULT ':' block_item_list { $$ = new DefaultCaseStatement($3); }
  ;

expression_statement
  : ';' { $$ = new EmptyStatement(); }
  | expression ';' { $$ = new ExpressionStatement($1); }
  ;

selection_statement
  : TIF '(' expression ')' compound_statement TELSE statement
    { $$ = new IfElseStatement ($3, $5, $7); }
  | TIF '(' expression ')' compound_statement { $$ = new IfStatement($3, $5); }
  | TSWITCH '(' expression ')' '{' case_statement_list_with_default '}'
    { $$ = new SwitchStatement($3, $6); }
  ;

iteration_statement
  : TWHILE '(' expression ')' statement { $$ = new WhileStatement($3, $5); }
  | TDO statement TWHILE '(' expression ')' ';' { $$ = new DoStatement($2, $5); }
  | TFOR '(' expression_statement expression ';' ')' statement
    { $$ = ForStatement::newWithNoIncrement($3, $4, $7, yylineno); }
  | TFOR '(' expression_statement expression ';' expression ')' statement
    { $$ = new ForStatement($3, $4, $6, $8, yylineno); }
  | TFOR '(' declaration expression ';' ')' statement
    { $$ = ForStatement::newWithNoIncrement($3, $4, $7, yylineno); }
  | TFOR '(' declaration expression ';' expression ')' statement
    { $$ = new ForStatement($3, $4, $6, $8, yylineno); }
  ;

jump_statement
  : TCONTINUE ';' { $$ = new ContinueStatement(); }
  | TBREAK ';' { $$ = new BreakStatement(); }
  | TRETURN ';' { $$ =  new ReturnVoidStatement(yylineno); }
  | TRETURN expression ';' { $$ = new ReturnStatement($2, yylineno); }
  ;

throw_statement
  : TTHROW expression ';' { $$ = new ThrowStatement($2, yylineno); }
  ;

exit_statement
  : TEXIT '(' expression ')' ';' { $$ = new ExitStatement($3, yylineno); }
  ;

try_catch_statement
  : TTRY compound_statement catch_block { 
    $$ = new TryCatchStatement($2, *$3); delete $3; 
  }
  ;

catch_block
  : catch_clause { $$ = new std::vector<Catch *>(); $$->push_back($1); }
  | catch_block catch_clause { $1->push_back($2); }
  ;

catch_clause
  : TCATCH '(' exception_type_specifier '*' TIDENTIFIER ')' compound_statement
    { $$ = new Catch($3, *$5, $7, yylineno); delete $5; }
  ;

variable_declaration
  : type_specifier identifier ';' { $$ = VariableDeclaration::create($1, $2, yylineno); }
  | type_specifier identifier '=' expression ';'
    { $$ = VariableDeclaration::createWithAssignment($1, $2, $4, yylineno); }
  ;

method_definition
  : access_specifier type_specifier TIDENTIFIER '(' method_definition_arguments ')'
      method_exceptions method_qualifiers compound_statement
    {
      $$ = new MethodDefinition($1, $2, *$3, *$5, *$7, *$8, $9, yylineno);
      delete $3;
      delete $5;
      delete $7;
      delete $8;
    }
  ;

static_method_definition
  : access_specifier TSTATIC type_specifier TIDENTIFIER '(' method_definition_arguments ')'
      method_exceptions compound_statement
    {
      $$ = new StaticMethodDefinition($1, $3, *$4, *$6, *$8, $9, yylineno);
      delete $4;
      delete $6;
      delete $8;
    }
  ;

interface_static_method_definition
  : TSTATIC type_specifier TIDENTIFIER '(' method_definition_arguments ')'
      method_exceptions compound_statement
    {
      AccessLevel accessLevel = AccessLevel::PUBLIC_ACCESS;
      $$ = new StaticMethodDefinition(accessLevel, $2, *$3, *$5, *$7, $8, yylineno);
      delete $3;
      delete $5;
      delete $7;
    }
  ;

method_qualifiers
  : /* blank */ { $$ = new MethodQualifierSet(); }
  | method_qualifier_set { $$ = $1; }
  ;

method_qualifier_set
  : method_qualifier { $$ = new MethodQualifierSet(); $$->insert($1); }
  | method_qualifier_set method_qualifier { $$ = $1; $$->insert($2); }
  ;

method_qualifier
  : TOVERRIDE { $$ = MethodQualifier::OVERRIDE; }
  | TEXPOSED { $$ = MethodQualifier::EXPOSED; }
  ;

method_exceptions
  : /* blank */ { $$ = new std::vector<IModelTypeSpecifier*>(); }
  | TTHROWS exception_type_specifier_list { $$ = $2; }
  ;

exception_type_specifier_list
  : exception_type_specifier { $$ = new std::vector<IModelTypeSpecifier*>(); $$->push_back($1); }
  | exception_type_specifier_list ',' exception_type_specifier { $1->push_back($3); }
  ;

method_argument_declaration
  : type_specifier identifier { $$ = VariableDeclaration::create($1, $2, yylineno); }
  ;

method_definition_arguments
  : /* blank */  { $$ = new VariableList(); }
  | method_argument_declaration
    { $$ = new VariableList(); $$->push_back($<variable_declaration>1); }
  | method_definition_arguments ',' method_argument_declaration
    { $1->push_back($<variable_declaration>3); }
  ;

access_specifier
  : TPUBLIC { $$ = AccessLevel::PUBLIC_ACCESS; }
  | TPRIVATE { $$ = AccessLevel::PRIVATE_ACCESS; }
  ;

constant_value
  : TCHAR { $$ = new CharConstant($1->c_str()[1]); delete $1; }
  | TINTEGER { $$ = new IntConstant(atol($1->c_str())); delete $1; }
  | TLONG { $$ = new LongConstant(atoll($1->c_str())); delete $1; }
  | TFLOAT { $$ = new FloatConstant(atof($1->c_str())); delete $1; }
  | TDOUBLE { $$ = new DoubleConstant(strtod($1->c_str(), NULL)); delete $1; }
  | TTRUE { $$ = new BooleanConstant(true); }
  | TFALSE { $$ = new BooleanConstant(false); }
  ;
    
string_literal
  : STRING_LITERAL { $$ = new StringLiteral($1->substr(1, $1->length() - 2)); delete $1; }
  ;

constant_reference
  : object_type_specifier_dot TCONSTANTIDENTIFIER
    { $$ = new ConstantReference($1, *$2); delete $2; }
  | TCONSTANTIDENTIFIER
    { $$ = new ConstantReference(NULL, *$1); delete $1; }
  ;

static_method_call
  : object_type_specifier_dot TIDENTIFIER '(' call_arguments ')'
    {
      $$ = new StaticMethodCall($1, *$2, *$4, yylineno);
      delete $2;
      delete $4;
    }
  ;

identifier
  : TIDENTIFIER { $$ = new Identifier(*$1); delete $1; }
  ;

field_identifier
  : TFIELDIDENTIFIER { $$ = new Identifier(*$1); delete $1; }
  ;

primary_expression
  : identifier { $$ = (IExpression*) $1; }
  | field_identifier
  | constant_value
  | constant_reference
  | string_literal
  | static_array_allocation
  | object_builder
  | injection
  | static_method_call
  | llvm_function_call
  | llvm_reference
  | thread_expression
  | '(' expression ')' { $$ = $2; }
  | TNULL { $$ = new NullExpression(); }
  ;

postfix_expression
  : primary_expression
  | postfix_expression '.' TIDENTIFIER { $$ = new IdentifierChain($1, *$3); delete $3; }
  | postfix_expression '[' expression ']' { $$ = new ArrayElementExpression($1, $3, yylineno); }
  | postfix_expression '(' call_arguments ')'
    { $$ = new MethodCall($1, *$3, yylineno); delete $3; }
  | postfix_expression TINCOP { $$ = IncrementExpression::newIncrementByOne($1, yylineno); }
  | postfix_expression TDECOP { $$ = IncrementExpression::newDecrementByOne($1, yylineno); }
  ;

unary_expression
  : postfix_expression
  | '-' cast_expression { $$ = new NegateExpression($2); }
  | '!' cast_expression { $$ = new BooleanNotExpression($2); }
  ;

cast_expression
  : unary_expression
  | '(' type_specifier ')' cast_expression { $$ = new CastExpression($2, $4, yylineno); }
  ;

multiplicative_expression
  : cast_expression
  | multiplicative_expression '*' cast_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '*', $3, yylineno); }
  | multiplicative_expression '/' cast_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '/', $3, yylineno); }
  | multiplicative_expression '%' cast_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '%', $3, yylineno); }
  ;

additive_expression
  : multiplicative_expression
  | additive_expression '+' multiplicative_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '+', $3, yylineno); }
  | additive_expression '-' multiplicative_expression
    { $$ = new AdditiveMultiplicativeExpression($1, '-', $3, yylineno); }
  ;

relational_expression
  : additive_expression
  | relational_expression '<' additive_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_LT, $3, yylineno); }
  | relational_expression '>' additive_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_GT, $3, yylineno); }
  | relational_expression TCLE additive_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_LE, $3, yylineno); }
  | relational_expression TCGE additive_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_GE, $3, yylineno); }
  ;

type_comparision_expression
  : relational_expression
  | type_comparision_expression TINSTANCEOF type_specifier
    { $$ = new TypeComparisionExpression($1, $3); }
  ;

equality_expression
  : type_comparision_expression
  | equality_expression TCEQ relational_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_EQ, $3, yylineno); }
  | equality_expression TCNE relational_expression
    { $$ = new RelationalExpression($1, RELATIONAL_OPERATION_NE, $3, yylineno); }
  ;

logical_and_expression
  : equality_expression
  | logical_and_expression TANDOP equality_expression { $$ = new LogicalAndExpression($1, $3); }
  ;

logical_or_expression
  : logical_and_expression
  | logical_or_expression TOROP logical_and_expression { $$ = new LogicalOrExpression($1, $3); }
  ;

conditional_expression
  : logical_or_expression
  | logical_or_expression '?' expression ':' conditional_expression
    { $$ = new ConditionalExpression($1, $3, $5); }
  ;

expression
  : conditional_expression
  | assignment
  | array_allocation
  ;

assignment
  : postfix_expression '=' expression { $$ = new Assignment($1, $3, yylineno); }
  ;

thread_expression
  : TTHREAD { $$ = new ThreadExpression(); }

array_allocation
  : TNEW array_specific_type_specifier { $$ = new ArrayAllocation($2); }
  ;

static_array_allocation
  : '{' static_array_elements '}' { $$ = new ArrayAllocationStatic(*$2, yylineno); delete $2; }
  ;

static_array_elements
  : expression { $$ = new ExpressionList(); $$->push_back($1); }
  | call_arguments ',' expression  { $1->push_back($3); $$ = $1; }
  | call_arguments ','  { $$ = $1; }
  ;

injection
  : TINJECTOR '(' object_type_specifier_injectable ')' injection_argument_list '.' TINJECT '(' ')'
    { $$ = new Injector($3, *$5, yylineno); delete $5; }
  | TINJECTOR '(' object_type_specifier_injectable ')' '.' TINJECT '(' ')'
    { InjectionArgumentList argumentList; $$ = new Injector($3, argumentList, yylineno); }
  ;

injection_argument_list
  : injection_argument { $$ = new InjectionArgumentList(); $$->push_back($1); }
  | injection_argument_list injection_argument { $1->push_back($2); }
  ;

injection_argument
  : '.' TIDENTIFIER '(' expression ')' { $$ = new InjectionArgument(*$2, $4); delete $2; }
  ;

object_builder
  : TBUILDER '(' object_type_specifier_buildable ')' object_builder_argument_list
    '.' TBUILD '(' ')'
    { $$ = new ObjectBuilder($3, *$5, yylineno); delete $5; }
  | TBUILDER '(' object_type_specifier_buildable ')' '.' TBUILD '(' ')'
    { ObjectBuilderArgumentList argumentList; $$ = new ObjectBuilder($3, argumentList, yylineno); }
  ;

object_builder_argument_list
  : object_builder_argument { $$ = new ObjectBuilderArgumentList(); $$->push_back($1); }
  | object_builder_argument_list object_builder_argument { $1->push_back($2); }
  ;

object_builder_argument
  : '.' TIDENTIFIER '(' expression ')' { $$ = new ObjectBuilderArgument(*$2, $4); delete $2; }
  ;

call_arguments
  : /* blank */  { $$ = new ExpressionList(); }
  | expression { $$ = new ExpressionList(); $$->push_back($1); }
  | call_arguments ',' expression  { $1->push_back($3); }
  ;

type_specifier
  : non_array_type_specifier
  | array_type_specifier { $$ = (ITypeSpecifier*) $1; }
  | array_owner_type_specifier { $$ = (ITypeSpecifier*) $1; }
  ;

// Compiler checks that injected type is not a primitive or a reference
injectable_type_specifier
  : non_array_type_specifier
  | array_specific_owner_type_specifier
  ;

non_array_type_specifier
  : primitive_type_specifier
  | object_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  | object_owner_type_specifier
  | native_type_specifier { $$ = (const ITypeSpecifier*) $1; }
  ;

primitive_type_specifier
  : TTYPEBOOLEAN { $$ = PrimitiveTypes::BOOLEAN_TYPE->newTypeSpecifier(); }
  | TTYPECHAR { $$ = PrimitiveTypes::CHAR_TYPE->newTypeSpecifier(); }
  | TTYPEDOUBLE { $$ = PrimitiveTypes::DOUBLE_TYPE->newTypeSpecifier(); }
  | TTYPEFLOAT { $$ = PrimitiveTypes::FLOAT_TYPE->newTypeSpecifier(); }
  | TTYPEINT { $$ = PrimitiveTypes::INT_TYPE->newTypeSpecifier(); }
  | TTYPELONG { $$ = PrimitiveTypes::LONG_TYPE->newTypeSpecifier(); }
  | TTYPESTRING { $$ = PrimitiveTypes::STRING_TYPE->newTypeSpecifier(); }
  | TTYPEVOID { $$ = PrimitiveTypes::VOID_TYPE->newTypeSpecifier(); }
  ;

array_owner_type_specifier
  : array_type_specifier '*'
    { $$ = new ArrayOwnerTypeSpecifier($1); }
  ;

array_type_specifier
  : non_array_type_specifier array_undefined_dimensions_list
    { $$ = new ArrayTypeSpecifier($1, $2); }
  ;

array_specific_owner_type_specifier
  : array_specific_type_specifier '*'
    { $$ = new ArraySpecificOwnerTypeSpecifier($1); }
  ;

array_specific_type_specifier
  : non_array_type_specifier array_dimensions
    { $$ = new ArraySpecificTypeSpecifier($1, *$2); delete $2; }
  ;

array_dimensions
  : '[' expression ']' 
    { 
      $$ = new std::list<const IExpression*>(); 
      $$->push_back($2); 
    }
  | array_dimensions '[' expression ']' 
    { $1->push_back($3); $$ = $1; }
  ;

array_undefined_dimensions_list
  : '[' ']' { $$ = 1u; }
  | array_undefined_dimensions_list '[' ']' { $$ = $1 + 1; }

object_owner_type_specifier
  : object_type_specifier '*' { $$ = new ObjectOwnerTypeSpecifier($1); }
  ;

object_type_specifier_dot
  : object_type_specifier_short '.'
  | object_type_specifier_top_level_full '.'
  | object_type_specifier_inner_short '.'
  | object_type_specifier_inner_full '.'
  ;

object_type_specifier
  : object_type_specifier_short
  | object_type_specifier_top_level_full
  | object_type_specifier_inner_short
  | object_type_specifier_inner_full
  ;

object_type_specifier_inner_short
  : model_type_specifier_inner_short { $$ = (IObjectTypeSpecifier*) $1; }
  | controller_type_specifier_inner_short { $$ = (IObjectTypeSpecifier*) $1; }
  | interface_type_specifier_inner_short { $$ = (IObjectTypeSpecifier*) $1; }
  | node_type_specifier_inner_short { $$ = (IObjectTypeSpecifier*) $1; }
  | thread_type_specifier_inner_short { $$ = (IObjectTypeSpecifier*) $1; }
  ;

object_type_specifier_inner_full
  : model_type_specifier_inner_full { $$ = (IObjectTypeSpecifier*) $1; }
  | controller_type_specifier_inner_full { $$ = (IObjectTypeSpecifier*) $1; }
  | interface_type_specifier_inner_full { $$ = (IObjectTypeSpecifier*) $1; }
  | node_type_specifier_inner_full { $$ = (IObjectTypeSpecifier*) $1; }
  | thread_type_specifier_inner_full { $$ = (IObjectTypeSpecifier*) $1; }
  ;

object_type_specifier_short
  : model_type_specifier_short { $$ = $1; }
  | controller_type_specifier_short { $$ = $1; }
  | interface_type_specifier_short { $$ = $1; }
  | node_type_specifier_short { $$ = $1; }
  | thread_type_specifier_short { $$ = $1; }
  ;

object_type_specifier_top_level_full
  : model_type_specifier_top_level_full { $$ = $1; }
  | controller_type_specifier_top_level_full { $$ = $1; }
  | interface_type_specifier_top_level_full { $$ = $1; }
  | node_type_specifier_top_level_full { $$ = $1; }
  | thread_type_specifier_top_level_full { $$ = $1; }
  ;

model_type_specifier
  : model_type_specifier_top_level
  | model_type_specifier_inner
  ;

model_type_specifier_full
  : model_type_specifier_top_level_full
  | model_type_specifier_inner_full
  ;

model_type_specifier_top_level
  : model_type_specifier_short
  | model_type_specifier_top_level_full
  ;

model_type_specifier_short
  : TMODELIDENTIFIER
    { $$ = new ModelTypeSpecifier(NULL, *$1, yylineno); delete $1; }
  ;

model_type_specifier_top_level_full
  : postfix_expression '.' TMODELIDENTIFIER 
    { $$ = new ModelTypeSpecifierFull($1, *$3, yylineno); delete $3; }
  ;

model_type_specifier_inner
  : model_type_specifier_inner_short
  | model_type_specifier_inner_full
  ;

model_type_specifier_inner_short
  : object_type_specifier_short '.' TMODELIDENTIFIER 
    { 
      $$ = new ModelTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

model_type_specifier_inner_full
  : object_type_specifier_top_level_full '.' TMODELIDENTIFIER 
    { 
      $$ = new ModelTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

controller_type_specifier
  : controller_type_specifier_top_level
  | controller_type_specifier_inner
  ;

controller_type_specifier_full
  : controller_type_specifier_top_level_full
  | controller_type_specifier_inner_full
  ;

controller_type_specifier_top_level
  : controller_type_specifier_short
  | controller_type_specifier_top_level_full
  ;

controller_type_specifier_short
  : TCONTROLLERIDENTIFIER 
    { $$ = new ControllerTypeSpecifier(NULL, *$1, yylineno); delete $1; }
  ;  

controller_type_specifier_top_level_full
  : postfix_expression '.' TCONTROLLERIDENTIFIER 
    { $$ = new ControllerTypeSpecifierFull($1, *$3, yylineno); delete $3; }
  ;

controller_type_specifier_inner
  : controller_type_specifier_inner_short
  | controller_type_specifier_inner_full
  ;

controller_type_specifier_inner_short
  : object_type_specifier_short '.' TCONTROLLERIDENTIFIER 
    { 
      $$ = new ControllerTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

controller_type_specifier_inner_full
  : object_type_specifier_top_level_full '.' TCONTROLLERIDENTIFIER 
    { 
      $$ = new ControllerTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

interface_type_specifier
  : interface_type_specifier_top_level
  | interface_type_specifier_inner
  ;

interface_type_specifier_full
  : interface_type_specifier_top_level_full
  | interface_type_specifier_inner_full
  ;

interface_type_specifier_top_level
  : interface_type_specifier_short
  | interface_type_specifier_top_level_full
  ;

interface_type_specifier_short
  : TINTERFACEIDENTIFIER 
    { $$ = new InterfaceTypeSpecifier(NULL, *$1, yylineno); delete $1; }
  ;

interface_type_specifier_top_level_full
  : postfix_expression '.' TINTERFACEIDENTIFIER 
    { $$ = new InterfaceTypeSpecifierFull($1, *$3, yylineno); delete $3; }
  ;

interface_type_specifier_inner
  : interface_type_specifier_inner_short
  | interface_type_specifier_inner_full
  ;

interface_type_specifier_inner_short
  : object_type_specifier_short '.' TINTERFACEIDENTIFIER 
    { 
      $$ = new InterfaceTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

interface_type_specifier_inner_full
  : object_type_specifier_top_level_full '.' TINTERFACEIDENTIFIER 
    { 
      $$ = new InterfaceTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

node_type_specifier
  : node_type_specifier_top_level
  | node_type_specifier_inner
  ;

node_type_specifier_full
  : node_type_specifier_top_level_full
  | node_type_specifier_inner_full
  ;

node_type_specifier_top_level
  : node_type_specifier_short
  | node_type_specifier_top_level_full
  ;

node_type_specifier_short
  : TNODEIDENTIFIER
    { $$ = new NodeTypeSpecifier(NULL, *$1, yylineno); delete $1; }
  ;  

node_type_specifier_top_level_full
  : postfix_expression '.' TNODEIDENTIFIER 
    { $$ = new NodeTypeSpecifierFull($1, *$3, yylineno); delete $3; }
  ;

node_type_specifier_inner
  : node_type_specifier_inner_short
  | node_type_specifier_inner_full
  ;

node_type_specifier_inner_short
  : object_type_specifier_short '.' TNODEIDENTIFIER 
    { 
      $$ = new NodeTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

node_type_specifier_inner_full
  : object_type_specifier_top_level_full '.' TNODEIDENTIFIER 
    { 
      $$ = new NodeTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

thread_type_specifier
  : thread_type_specifier_top_level
  | thread_type_specifier_inner
  ;

thread_type_specifier_full
  : thread_type_specifier_top_level_full
  | thread_type_specifier_inner_full
  ;

thread_type_specifier_top_level
  : thread_type_specifier_short
  | thread_type_specifier_top_level_full
  ;

thread_type_specifier_short
  : TTHREADIDENTIFIER
    { $$ = new ThreadTypeSpecifier(NULL, *$1, yylineno); delete $1; }
  ;

thread_type_specifier_top_level_full
  : postfix_expression '.' TTHREADIDENTIFIER 
    { $$ = new ThreadTypeSpecifierFull($1, *$3, yylineno); delete $3; }
  ;

thread_type_specifier_inner
  : thread_type_specifier_inner_short
  | thread_type_specifier_inner_full
  ;

thread_type_specifier_inner_short
  : object_type_specifier_short '.' TTHREADIDENTIFIER 
    { 
      $$ = new ThreadTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

thread_type_specifier_inner_full
  : object_type_specifier_top_level_full '.' TTHREADIDENTIFIER 
    { 
      $$ = new ThreadTypeSpecifier($1->takePackage(), $1->getShortName() + "." + *$3, yylineno);
      delete $1; 
      delete $3; 
    }
  ;

exception_type_specifier
  : model_type_specifier { $$ = (ModelTypeSpecifier*) $1; }
  ;

object_type_specifier_buildable
  : model_type_specifier { $$ = (IObjectTypeSpecifier*) $1; }
  | node_type_specifier { $$ = (IObjectTypeSpecifier*) $1; }
  ;

object_type_specifier_injectable
  : interface_type_specifier { $$ = (IObjectTypeSpecifier*) $1; }
  | controller_type_specifier { $$ = (IObjectTypeSpecifier*) $1; }
  | thread_type_specifier { $$ = (IObjectTypeSpecifier*) $1; }
  ;

%%


