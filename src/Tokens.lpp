%{
  #include <string>
  #include "wisey/AccessLevel.hpp"
  #include "wisey/Catch.hpp"
  #include "wisey/CompoundStatement.hpp"
  #include "wisey/ControllerFieldDeclaration.hpp"
  #include "wisey/ControllerTypeSpecifier.hpp"
  #include "wisey/CaseStatement.hpp"
  #include "wisey/DefaultCaseStatement.hpp"
  #include "wisey/Identifier.hpp"
  #include "wisey/InterfaceTypeSpecifier.hpp"
  #include "wisey/MethodDeclaration.hpp"
  #include "wisey/MethodSignatureDeclaration.hpp"
  #include "wisey/ModelBuilderArgument.hpp"
  #include "wisey/ModelFieldDeclaration.hpp"
  #include "wisey/ModelTypeSpecifier.hpp"
  #include "wisey/PrimitiveTypeSpecifier.hpp"
  #include "wisey/ProgramFile.hpp"
  #include "wisey/SwitchStatement.hpp"
  #include "wisey/VariableDeclaration.hpp"
  #include "y.tab.h"
  #define SAVE_TOKEN yylval.string = new std::string(yytext, yyleng)
  #define TOKEN(t) (yylval.token = t)
  extern "C" int yywrap() { return 1; }
%}

D   [0-9]
NZ  [1-9]
IS  (l|L)
DS  (d|D)
E   ([Ee][+-]?{D}+)
ES  (\\(['"\?\\abfnrtv]|[0-7]{1,3}|x[a-fA-F0-9]+))
WS  [ \t\v\n\f]

%option yylineno
%option nounput

%%

[ \t\n]                       ;
"boolean"                     return TOKEN(TTYPEBOOLEAN);
"char"                        return TOKEN(TTYPECHAR);
"int"                         return TOKEN(TTYPEINT);
"long"                        return TOKEN(TTYPELONG);
"float"                       return TOKEN(TTYPEFLOAT);
"double"                      return TOKEN(TTYPEDOUBLE);
"void"                        return TOKEN(TTYPEVOID);
"return"                      return TOKEN(TRETURN);
"while"                       return TOKEN(TWHILE);
"for"                         return TOKEN(TFOR);
"if"                          return TOKEN(TIF);
"else"                        return TOKEN(TELSE);
"break"                       return TOKEN(TBREAK);
"continue"                    return TOKEN(TCONTINUE);
"switch"                      return TOKEN(TSWITCH);
"case"                        return TOKEN(TCASE);
"default"                     return TOKEN(TDEFAULT);
"fallthrough"                 return TOKEN(TFALLTHROUGH);
"do"                          return TOKEN(TDO);
"model"                       return TOKEN(TMODEL);
"interface"                   return TOKEN(TINTERFACE);
"implements"                  return TOKEN(TIMPLEMENTS);
"extends"                     return TOKEN(TEXTENDS);
"controller"                  return TOKEN(TCONTROLLER);
"receive"                     return TOKEN(TRECEIVE);
"inject"                      return TOKEN(TINJECT);
"state"                       return TOKEN(TSTATE);
"true"                        return TOKEN(TTRUE);
"false"                       return TOKEN(TFALSE);
"builder"                     return TOKEN(TBUILDER);
"build"                       return TOKEN(TBUILD);
"public"                      return TOKEN(TPUBLIC);
"private"                     return TOKEN(TPRIVATE);
"instanceof"                  return TOKEN(TINSTANCEOF);
"throw"                       return TOKEN(TTHROW);
"throws"                      return TOKEN(TTHROWS);
"try"                         return TOKEN(TTRY);
"catch"                       return TOKEN(TCATCH);
"finally"                     return TOKEN(TFINALLY);
"bind"                        return TOKEN(TBIND);
"to"                          return TOKEN(TTO);
"package"                     return TOKEN(TPACKAGE);
"import"                      return TOKEN(TIMPORT);
"="                           return TOKEN('=');
"=="                          return TOKEN(TCEQ);
"!="                          return TOKEN(TCNE);
"<"                           return TOKEN('<');
"<="                          return TOKEN(TCLE);
">"                           return TOKEN('>');
">="                          return TOKEN(TCGE);
"++"                          return TOKEN(TINCOP);
"--"                          return TOKEN(TDECOP);
"("                           return TOKEN('(');
")"                           return TOKEN(')');
"{"                           return TOKEN('{');
"}"                           return TOKEN('}');
"."                           return TOKEN('.');
","                           return TOKEN(',');
"+"                           return TOKEN('+');
"-"                           return TOKEN('-');
"*"                           return TOKEN('*');
"/"                           return TOKEN('/');
":"                           return TOKEN(':');
"?"                           return TOKEN('?');
";"                           return TOKEN(';');
"!"                           return TOKEN('!');
"&&"                          return TOKEN(TANDOP);
"||"                          return TOKEN(TOROP);
M[A-Z][a-zA-Z0-9_]*           SAVE_TOKEN; return TMODELIDENTIFIER;
I[A-Z][a-zA-Z0-9_]*           SAVE_TOKEN; return TINTERFACEIDENTIFIER;
C[A-Z][a-zA-Z0-9_]*           SAVE_TOKEN; return TCONTROLLERIDENTIFIER;
m[A-Z][a-zA-Z0-9_]*           SAVE_TOKEN; return TFIELDIDENTIFIER;
[a-zA-Z_][a-zA-Z0-9_]*        SAVE_TOKEN; return TIDENTIFIER;
{D}*"."{D}+{E}?{DS}           SAVE_TOKEN; return TDOUBLE;
{D}+"."{E}?{DS}               SAVE_TOKEN; return TDOUBLE;
{D}*"."{D}+{E}?               SAVE_TOKEN; return TFLOAT;
{D}+"."{E}?                   SAVE_TOKEN; return TFLOAT;
{D}+{IS}                      SAVE_TOKEN; return TLONG;
{D}+                          SAVE_TOKEN; return TINTEGER;
"'"([^'\\\n]|{ES})+"'"        SAVE_TOKEN; return TCHAR;
(\"([^"\\\n]|{ES})*\"{WS}*)+  SAVE_TOKEN; return STRING_LITERAL;
.                             printf("Unknown token!n"); yyterminate();

%%

