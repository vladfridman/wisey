%{
  #include <string>
  #include "yazyk/Block.hpp"
  #include "yazyk/CompoundStatement.hpp"
  #include "yazyk/CaseStatement.hpp"
  #include "yazyk/DefaultCaseStatement.hpp"
  #include "yazyk/Identifier.hpp"
  #include "yazyk/PrimitiveTypeSpecifier.hpp"
  #include "yazyk/SwitchStatement.hpp"
  #include "yazyk/VariableDeclaration.hpp"
  #include "y.tab.h"
  #define SAVE_TOKEN yylval.string = new std::string(yytext, yyleng)
  #define TOKEN(t) (yylval.token = t)
  extern "C" int yywrap() { return 1; }
%}

D   [0-9]
NZ  [1-9]
IS  (i64|I64)
DS  (f64|F64)
E   ([Ee][+-]?{D}+)
ES  (\\(['"\?\\abfnrtv]|[0-7]{1,3}|x[a-fA-F0-9]+))
WS  [ \t\v\n\f]

%option yylineno
%option nounput

%%

[ \t\n]                       ;
"int32"                       return TOKEN(TTYPEINT32);
"int64"                       return TOKEN(TTYPEINT64);
"float32"                     return TOKEN(TTYPEFLOAT32);
"float64"                     return TOKEN(TTYPEFLOAT64);
"void"                        return TOKEN(TTYPEVOID);
"return"                      return TOKEN(TRETURN);
"while"                       return TOKEN(TWHILE);
"for"                         return TOKEN(TFOR);
"if"                          return TOKEN(TIF);
"else"                        return TOKEN(TELSE);
"break"                       return TOKEN(TBREAK);
"continue"                    return TOKEN(TCONTINUE);
"switch"                      return TOKEN(TSWITCH);
"case"                        return TOKEN(TCASE);
"default"                     return TOKEN(TDEFAULT);
"fallthrough"                 return TOKEN(TFALLTHROUGH);
"do"                          return TOKEN(TDO);
"="                           return TOKEN('=');
"=="                          return TOKEN(TCEQ);
"!="                          return TOKEN(TCNE);
"<"                           return TOKEN('<');
"<="                          return TOKEN(TCLE);
">"                           return TOKEN('>');
">="                          return TOKEN(TCGE);
"++"                          return TOKEN(TINCOP);
"--"                          return TOKEN(TDECOP);
"("                           return TOKEN('(');
")"                           return TOKEN(')');
"{"                           return TOKEN('{');
"}"                           return TOKEN('}');
"."                           return TOKEN('.');
","                           return TOKEN(',');
"+"                           return TOKEN('+');
"-"                           return TOKEN('-');
"*"                           return TOKEN('*');
"/"                           return TOKEN('/');
":"                           return TOKEN(':');
"?"                           return TOKEN('?');
";"                           return TOKEN(';');
"&&"                          return TOKEN(TANDOP);
"||"                          return TOKEN(TOROP);
[a-zA-Z_][a-zA-Z0-9_]*        SAVE_TOKEN; return TIDENTIFIER;
{D}*"."{D}+{E}?{DS}           SAVE_TOKEN; return TDOUBLE;
{D}+"."{E}?{DS}               SAVE_TOKEN; return TDOUBLE;
{D}*"."{D}+{E}?               SAVE_TOKEN; return TFLOAT;
{D}+"."{E}?                   SAVE_TOKEN; return TFLOAT;
{D}+{IS}                      SAVE_TOKEN; return TLONG;
{D}+                          SAVE_TOKEN; return TINTEGER;
"'"([^'\\\n]|{ES})+"'"        SAVE_TOKEN; return TCHAR;
(\"([^"\\\n]|{ES})*\"{WS}*)+  SAVE_TOKEN; return STRING_LITERAL;
.                             printf("Unknown token!n"); yyterminate();

%%

