package systems.vos.wisey.compiler.tests;

import wisey.lang.IProgram;
import wisey.lang.CCallStack;
import wisey.lang.threads.CDefaultProducer;
import wisey.lang.threads.IProducerThread;
import wisey.lang.threads.IThread;

thread TProducer implements IProducerThread {
  inject CDefaultProducer* mDefaultProducer;
  receive int mSize;

  public void run() conceal {
    sleep(1);
    printout("This is producer thread!\n");
    for (int i = 0; i < 2; i++) {
      MProducerResult* result = builder(MProducerResult).withResult(i).build();
      printout("Adding result " + i + "\n");
      result = thread.send(result);
      while (result != null) {
        printout("Could not add result " + i + ", sleeping\n");
        sleep(1);
        result = thread.send(result);
      }
    }
  }

  public void start() reveal {
    mDefaultProducer.start(this, mSize);
  }

  public boolean isStarted() reveal {
    return mDefaultProducer.isStarted();
  }

  public boolean isFinished() reveal {
    return mDefaultProducer.isFinished();
  }

  public boolean isCancelled() reveal {
    return mDefaultProducer.isCancelled();
  }

  public boolean hasResult() reveal {
    return mDefaultProducer.hasResult();
  }

  public void cancel() reveal {
    mDefaultProducer.cancel();
  }

  public void reset() reveal {
    mDefaultProducer.reset();
  }

  public ::wisey::model* consume() reveal {
    return mDefaultProducer.consume();
  }

  public ::wisey::model* send(::wisey::model* message) conceal {
    return mDefaultProducer.send(message);
  }

  public CCallStack getCallStack() conceal {
    return mDefaultProducer.getCallStack();
  }

  public void exit() conceal {
    mDefaultProducer.exit();
  }

  public model MProducerResult {
    int mResult;

    public int getResult() {
      return mResult;
    }
  }
}

controller CProgram implements IProgram {
  inject TProducer* mProducer.withSize(3);

  public int run() {
    printout("Thread started = " + mProducer.isStarted() + ", has result = " + mProducer.hasResult() + "\n");
    printout("Starting producer thread\n");
    mProducer.start();
    printout("Producer thread started\n");
    printout("Thread started = " + mProducer.isStarted() + ", has result = " + mProducer.hasResult() + "\n");
    IThread.sleep(2);
    while (mProducer.hasResult()) {
      int result = ((TProducer.MProducerResult) mProducer.consume()).getResult();
      printout("Producer thread finished with result: " + result + "\n");
      printout("Thread started = " + mProducer.isStarted() + ", has result = " + mProducer.hasResult() + "\n");
      IThread.sleep(1);
    }

    mProducer.reset();
    printout("Thread started = " + mProducer.isStarted() + ", has result = " + mProducer.hasResult() + "\n");
    printout("Starting producer thread\n");
    mProducer.start();
    printout("Producer thread started\n");
    printout("Thread started = " + mProducer.isStarted() + ", has result = " + mProducer.hasResult() + "\n");
    IThread.sleep(2);
    while (mProducer.hasResult()) {
      int result = ((TProducer.MProducerResult) mProducer.consume()).getResult();
      printout("Producer thread finished with result: " + result + "\n");
      printout("Thread started = " + mProducer.isStarted() + ", has result = " + mProducer.hasResult() + "\n");
      IThread.sleep(1);
    }
    mProducer.cancel();
    return 0;
  }
}

bind(IProgram).to(CProgram);
