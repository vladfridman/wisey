package wisey.threads;

import wisey.data.CReferenceMap;
import wisey.data.COwnerMap;

/**
 * Manages instances of context injected objects
 */
controller CContextManager {
  inject CReferenceMap* mContextNameToContextMap;
  inject COwnerMap* mContextToObjectMap;

  /**
   * Find the existing instance of the object with the given name in the given context
   */
  public ::wisey::object getInstance(string contextName, string objectName) {
    ::wisey::object contextObject = mContextNameToContextMap.get(contextName);
    if (contextObject == null) {
      throw builder(MContextNonExistant).withContextName(contextName).build();
    }
    CReferenceMap objectNameToObjectMap = mContextToObjectMap.get(contextObject);
    return objectNameToObjectMap.get(objectName);
  }

  /**
   * Set current context to the given object value
   */
  public void setContext(string contextName, ::wisey::object contextObject) {
    mContextNameToContextMap.put(contextName, contextObject);
    CReferenceMap objectNameToObjectMap = mContextToObjectMap.get(contextObject);
    if (objectNameToObjectMap == null) {
      mContextToObjectMap.put(contextObject, CReferenceMap.create());
    }
  }

  /**
   * Removes all objects stored in the given context
   */
  public void eraseContext(string contextName) {
    ::wisey::object contextObject = mContextNameToContextMap.get(contextName);
    if (contextObject == null) {
      return;
    }
    mContextToObjectMap.erase(contextObject);
    mContextNameToContextMap.erase(contextName);
  }

  /**
   * Set injected object instance in the given context
   */
  public void setInstance(string contextName, string objectName, ::wisey::object instance) {
    ::wisey::object contextObject = mContextNameToContextMap.get(contextName);
    if (contextObject == null) {
      throw builder(MContextNonExistant).withContextName(contextName).build();
    }
    CReferenceMap objectNameToObjectMap = mContextToObjectMap.get(contextObject);
    objectNameToObjectMap.put(objectName, instance);
  }

  /**
   * Clears the context manager maps
   */
  public void clear() {
    mContextToObjectMap.clear();
    mContextNameToContextMap.clear();
  }
}