package wisey.io;

import wisey.lang.CString;
import wisey.lang.MString;

/**
 * Reads lines from standard input
 */
controller CInputReader {

  public constant int BUFFER_SIZE = 256;

  inject char[BUFFER_SIZE]* mBuffer;

  public CString* readLine() {
    CString* line = CString.create();
    ::llvm::i8::pointer result = null;
    long length = 0;

    do {
      result = ::llvm::function::fgets(mBuffer, BUFFER_SIZE, ::llvm::global::__stdinp);
      if (result == null) {
        break;
      }
      
      length = ::llvm::function::strlen(mBuffer);
      line.appendCharArray(mBuffer);
    } while (result != null && length > 0 && mBuffer[length - 1] != '\n');

    if (!line.getLength()) {
      return null;
    }

    return line;
  }
}