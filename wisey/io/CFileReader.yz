package wisey.io;

import wisey.lang.CString;
import wisey.lang.MString;

/**
 * Reads lines from a text file
 */
controller CFileReader {

  public constant int BUFFER_SIZE = 256;

  inject CFile* mFile;
  inject char[BUFFER_SIZE]* mBuffer;

  receive MString* mFileName;

  public static CFileReader* create(CString fileName) {
    return inject(CFileReader).withFileName(fileName.toModel()).onHeap();
  }

  public CString* readLine() throws MFileOpenException, MFileCouldNotOpenException {
    if (!mFile.isOpened()) {
      mFile.openForReading(mFileName.toController());
    }

    CString* cstring = CString.create();
    ::llvm::i8::pointer result = null;
    long length = 0;

    do {
      result = ::llvm::function::fgets(mBuffer, BUFFER_SIZE, mFile.getFileStruct());
      length = ::llvm::function::strlen(mBuffer);
      cstring.appendCharArray(mBuffer);
    } while (length > 0 && mBuffer[length - 1] != '\n');

    if (!result) {
      mFile.close();
      return null;
    }

    return cstring;
  }
}