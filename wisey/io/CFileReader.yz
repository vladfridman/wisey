package wisey.io;

import wisey.lang.CString;
import wisey.lang.MString;

/**
 * Reads lines from a text file
 */
controller CFileReader {

  public constant int BUFFER_SIZE = 256;

  inject CFile* mFile;
  receive MString* mFileName;

  /**
   * Create an instance of the CFileReader to read the given file
   */
  public static CFileReader* create(CString fileName) {
    return inject(CFileReader).withFileName(fileName.toModel()).onHeap();
  }

  /**
   * Read one line up to the new line character or end of file
   * 
   * New line character will not be part of the returned string
   */
  public CString* readLine() throws MFileOpenException, MFileCouldNotOpenException {
    if (!mFile.isOpened()) {
      mFile.openForReading(mFileName.toController());
    }

    CString* line = readLineFromFile(mFile.getFileStruct());

    if (!line) {
      mFile.close();
    }

    return line;
  }

  public static CString* readLineFromFile(::llvm::struct::__sFILE::pointer fileStruct) {
    char[]* buffer = new char[BUFFER_SIZE];
    CString* line = CString.create();
    ::llvm::i8::pointer result = null;
    long length = 0;
    long bufferSize = buffer.getSize();

    do {
      result = ::llvm::function::fgets(buffer, BUFFER_SIZE, fileStruct);
      if (result == null) {
        break;
      }
      
      length = ::llvm::function::strlen(buffer);
      if (buffer[length - 1] == '\n') {
        buffer[length - 1] = '\0';
      }
      line.appendCharArray(buffer);
    } while (length > 0 && buffer[length - 1] != '\0');

    if (!line.getLength()) {
      return null;
    }

    return line;
  } 
}