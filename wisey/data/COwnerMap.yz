package wisey.data;

/**
 * Map of owner references to wisey objects
 */
controller COwnerMap {

  inject CPointerMap* mPointerMap.withSize(CPointerMap.DEFAULT_SIZE);

  /**
   * Creates a hashmap of deafault size
   */
  public static COwnerMap* create() {
    return injector(COwnerMap).inject();
  }

  /**
   * Puts a reference to an object into the map according keyed by the given key
   */
  public void put(::llvm::i8::pointer key, ::wisey::object* value) {
    COwnerList.NElement* element = builder(COwnerList.NElement)
      .withKey(key)
      .withValue(value)
      .build();
    mPointerMap.put(key, element);
  }

  /**
   * Looks up a reference to an object given by the key
   */
  public ::wisey::object get(::llvm::i8::pointer key) {
    CPointerList.IListElement element = mPointerMap.get(key);
    return element ? ((COwnerList.NElement) element).getValue() : null;
  }

  /**
   * Erases entries in the hashmap with the given key
   */
  public void erase(::llvm::i8::pointer key) {
    mPointerMap.erase(key);
  }

  /**
   * Clears the map
   */
  public void clear() {
    mPointerMap.clear();
  }
}