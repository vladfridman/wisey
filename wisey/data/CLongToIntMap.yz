package wisey.data;

/**
 * Map of ints keyed by longs
 */
controller CLongToIntMap {

  inject immediate ::llvm::i8::pointer* mMap.withConstructor(mapCreate).withDestructor(mapDestroy);

  /**
   * Puts a reference to an object into the map according keyed by the given key
   */
  public void put(long key, int value) {
    ::llvm::function::stl_long_to_int_map_put(mMap, key, value);
  }

  /**
   * Adds a value to the value contained in the map with the given key
   */
  public void addTo(long key, int value) {
    ::llvm::function::stl_long_to_int_map_addTo(mMap, key, value);
  }

  /**
   * Looks up a reference to an object given by the key
   */
  public int get(long key) throws MMapEntryDoesNotExistException {
    if (!::llvm::function::stl_long_to_int_map_has(mMap, key)) {
      throw build(MMapEntryDoesNotExistException).onHeap();
    }
    return ::llvm::function::stl_long_to_int_map_get(mMap, key);
  }

  /**
   * Tells whether an entry with the given key exists
   */
  public boolean has(long key) throws MMapEntryDoesNotExistException {
    return ::llvm::function::stl_long_to_int_map_has(mMap, key);
  }

  /**
   * Creates an instance of the CLongToIntMap
   */
  public static CLongToIntMap* create() {
    return inject(CLongToIntMap).onHeap();
  }

  /**
   * Erases entries in the hashmap with the given key
   */
  public void erase(long key) {
    ::llvm::function::stl_long_to_int_map_erase(mMap, key);
  }

  /**
   * Clears the map
   */
  public void clear() {
    ::llvm::function::stl_long_to_int_map_clear(mMap);
  }

  /**
   * Returns map size
   */
  public long getSize() {
    return ::llvm::function::stl_long_to_int_map_size(mMap);
  }

  /**
   * Returns an array containing map values
   */
  public int[]* getValues() {
    return ::llvm::function::stl_long_to_int_map_get_values_array(mMap);
  }

  /**
   * Returns an array containing map keys
   */
  public long[]* getKeys() {
    return ::llvm::function::stl_long_to_int_map_get_keys_array(mMap);
  }

  /**
   * Convert the map into an immutable model
   */
  public static MLongToIntMap* toModel(CLongToIntMap* map) {
    return map.convertToModel();
  }

  private MLongToIntMap* convertToModel() {
    return build(MLongToIntMap).withMap(mMap).onHeap();
  }

  private ::llvm::function ::llvm::i8::pointer mapCreate() {
    return ::llvm::function::stl_long_to_int_map_create();
  }

  private ::llvm::function ::llvm::void mapDestroy(::llvm::i8::pointer map) {
    ::llvm::function::stl_long_to_int_map_clear(map);
    ::llvm::function::stl_long_to_int_map_destroy(map);
  }
}