package wisey.data;

import wisey.lang.CMemoryPool;

/**
 * Map of references to wisey objects
 */
controller CReferenceMap {

  public constant long DEFAULT_SIZE = 109l;

  receive long mSize;
  inject immediate CPointerMap* mPointerMap.withSize(mSize);
  inject immediate CMemoryPool* mMemoryPool;

  /**
   * Creates a hashmap of deafault size
   */
  public static CReferenceMap* create() {
    return injector(CReferenceMap).withSize(DEFAULT_SIZE).inject();
  }

  /**
   * Creates a hashmap of a given size
   */
  public static CReferenceMap* createWithSize(long size) {
    return injector(CReferenceMap).withSize(size).inject();
  }

  /**
   * Puts a reference to an object into the map according keyed by the given key
   */
  public void put(::llvm::i8::pointer key, ::wisey::object value) {
    CReferenceList.NElement* element = build(CReferenceList.NElement)
      .withKey(key)
      .withValue(value)
      .onPool(mMemoryPool);
    mPointerMap.put(key, element);
  }

  /**
   * Looks up a reference to an object given by the key
   */
  public ::wisey::object get(::llvm::i8::pointer key) {
    CPointerList.IListElement element = mPointerMap.get(key);
    return element ? ((CReferenceList.NElement) element).getValue() : null;
  }

  /**
   * Erases entries in the hashmap with the given key
   */
  public void erase(::llvm::i8::pointer key) {
    mPointerMap.erase(key);
  }

  /**
   * Clears the map
   */
  public void clear() {
    mPointerMap.clear();
  }
}