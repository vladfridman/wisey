package wisey.lang.threads;

import wisey.lang.CCallStack;

/**
 * Default implementation of the producer thread.
 *
 * A producer thread can be in one of 4 states: Not started, Started, Cancelled, Finished
 * Before thread can be started again after it has been cancelled or finished it has to be reset
 */
controller CProducerThread implements IProducerThread {
  inject CProducerThreadRunner* mProducerThreadRunner;
  inject CCallStack* mCallStack;
  state IProducer* mProducer;

  public void run() override conceal {
    mProducer.produce();
  }

  /**
   * Starts the given thread with the results buffer of the given size and producer implementation
   *
   * Calling this method after the thread has already been started has no effect
   * This is called from an outside thread through a reveal method
   */
  public void start(::wisey::model* producer, int size) reveal {
    mProducer = producer;
    mProducerThreadRunner.createThread(this, size);
  }

  /**
   * Tells whether the thread has been started
   *
   * This is called from an outside thread through a reveal method
   */
  public boolean isStarted() override reveal {
    return mProducerThreadRunner.isStarted();
  }

  /**
   * Tells whether the thread has finished execution and has the result
   *
   * This is called from an outside thread through a reveal method
   */
  public boolean isFinished() override reveal {
    return mProducerThreadRunner.isFinished();
  }

  /**
   * Tells whether the thread execution has been cancelled
   *
   * This is called from an outside thread through a reveal method
   */
  public boolean isCancelled() override reveal {
    return mProducerThreadRunner.isCancelled();
  }

  /**
   * Cancel thread execution from antoher thread
   * If the thread has been already canceled or finished or not started then this has no effect
   *
   * This is called from an outside thread through a reveal method
   */
  public void cancel() override reveal {
    mProducerThreadRunner.cancelThread();
  }

  /**
   * Reset the thread to its initial state so it can be started again
   * Calling this when the thread is in Started or Not Started state has no effect
   *
   * This is called from an outside thread through a reveal method
   */
  public void reset() override reveal {
    mProducerThreadRunner.resetThread();
  }

  /**
   * Tells whether the thread has a result in its buffer that can be consumed
   * If the thread is in not started or cancelled state it returns false
   *
   * This is called from an outside thread through a reveal method
   */
  public boolean hasResult() override reveal {
    return mProducerThreadRunner.hasResult();
  }

  /**
   * Consumes the one result object from the result buffer
   * If the thread is Not started or Cancelled state returns null
   *
   * This is called from an outside thread through a reveal method
   */
  public ::wisey::model* consume() override reveal {
    return mProducerThreadRunner.consumeResult();
  }

  /**
   * Exits thread with null as a result. This is called from within the thread
   *
   * This is called from within the thread through a conceal method
   */
  public void exit() override conceal {
    mProducerThreadRunner.exitThread();
  }

  /**
   * Adds a result to the buffer of thread results
   *
   * This is called from within the thread through a conceal method
   */
  public ::wisey::model* send(::wisey::model* message) override conceal {
    return mProducerThreadRunner.addResult(message);
  }

  /**
   * Returns the callstack controller for the worker thread
   *
   * This is called from within the thread through a conceal method
   */
  public CCallStack getCallStack() override conceal {
    return mCallStack;
  }
}
