package wisey.lang.threads;

import wisey.lang.CCallStack;

/**
 * Uses pthread library to enable thread functionality
 */
controller CWorkerThreadRunner {
  state ::llvm::struct::_opaque_pthread_t::pointer mMyNativeThread;
  state boolean mIsCanceled;
  state ::llvm::object* mResult;

  /**
   * This native function bridges work() method of the thread object with the i8* (i8*)* function 
   * parameter of the posix threads interface.
   */
  ::llvm::function ::llvm::i8::pointer workBridge(::llvm::i8::pointer argument) {
    thread = (IThread) ((IWorkerThread) argument);
    callstack = injector(CCallStack).inject();
    callstack = thread.getCallStack();
    ((IWorkerThread) argument).work();

    return argument;
  }

  /**
   * Creates a new posix thread that calls workBridge function that in turn calls work method
   */
  public void createThread(IWorkerThread workerThread) {
    ::llvm::function::pthread_create(::llvm::reference(mMyNativeThread), 
                                     (::llvm::struct::_opaque_pthread_attr_t::pointer) null, 
                                     workBridge, 
                                     (::llvm::i8::pointer) workerThread);
  }

  /**
   * Waits for thread to finish its work
   */
  public void joinThread() {
    if (!mIsCanceled) {
      ::llvm::function::pthread_join(mMyNativeThread, (::llvm::i8::pointer::pointer) null);
    }
  }

  /**
   * Exits thread
   */
  public void exitThread() {
    ::llvm::function::pthread_exit((::llvm::i8::pointer) null);
  }

  /**
   * Cancel execution of the worker thread
   */
  public void cancelThread() {
    ::llvm::function::pthread_cancel(mMyNativeThread);
    mIsCanceled = true;
  }

  /**
   * Tells whether thread has started
   */
  public boolean hasStarted() {
    return mMyNativeThread != null;
  }

  /**
   * Tells whether the worker thread has been cancelled
   */
  public boolean wasCancelled() {
    return mIsCanceled;
  }

  /**
   * Set result of the thread
   */
  public void setResult(::llvm::object* result) {
    mResult = result;
  }

  /**
   * Return the result
   */
  public ::llvm::object* getResult() {
    return mResult;
  }

  /**
   * Tells whether the thread has prepared the result
   */
  public boolean hasResult() {
    return mResult != null;
  }
}