package wisey.lang.threads;

import wisey.lang.CCallStack;

/**
 * Default implementation of the worker thread.
 *
 * A worker thread can be in one of 4 states: Not started, Started, Cancelled, Finished
 * Before thread can be started again after it has been cancelled or finished it has to be reset
 */
controller CWorkerThread implements IThread {
  inject CWorkerThreadRunner* mWorkerThreadRunner;
  inject CCallStack* mCallStack;
  state IWorker* mWorker;

  /**
   * Starts the given thread with the given worker implementation
   *
   * Calling this method after the thread has already been started has no effect
   * This is called from an outside thread through a reveal method
   */
  public void start(::wisey::model* worker) {
    outsideThreadGuard(this);
    mWorker = worker;
    mWorkerThreadRunner.createThread(this);
  }

  /**
   * Tells whether the thread has been started
   */
  public boolean isStarted() {
    outsideThreadGuard(this);
    return mWorkerThreadRunner.isStarted();
  }

  /**
   * Tells whether the thread has finished execution and has the result
   */
  public boolean isFinished() {
    outsideThreadGuard(this);
    return mWorkerThreadRunner.isFinished();
  }

  /**
   * Tells whether the thread execution has been cancelled
   */
  public boolean isCancelled() {
    outsideThreadGuard(this);
    return mWorkerThreadRunner.isCancelled();
  }

  /**
   * Waits for the thread to finish and returns back the result
   * If the thread has been canceled or finished or not started then returns result immediately
   */
  public ::wisey::model* awaitResult() {
    outsideThreadGuard(this);
    mWorkerThreadRunner.joinThread();

    return mWorkerThreadRunner.getResult();
  }

  /**
   * Cancel thread execution from antoher thread
   * If the thread has been already canceled or finished or not started then this has no effect
   */
  public void cancel() {
    outsideThreadGuard(this);
    mWorkerThreadRunner.cancelThread();
  }

  /**
   * Reset the thread to its initial state so it can be started again
   * Calling this when the thread is in Started or Not Started state has no effect
   */
  public void reset() {
    outsideThreadGuard(this);
    mWorkerThreadRunner.resetThread();
  }

  public void exit() override {
    insideThreadGuard(this);
    mWorkerThreadRunner.exitThread();
  }

  public ::wisey::model* send(::wisey::model* message) override {
    insideThreadGuard(this);
    mWorkerThreadRunner.setResult(message);
    return null;
  }

  public CCallStack getCallStack() override {
    insideThreadGuard(this);
    return mCallStack;
  }

  public void run() override {
    insideThreadGuard(this);
    mWorker.work();
  }
}