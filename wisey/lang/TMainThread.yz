package wisey.lang;

import wisey.lang.threads.CDefaultWorker;
import wisey.lang.threads.IWorkerThread;

/**
 * Represents main thread where the program runs
 */
thread TMainThread implements IWorkerThread {
  inject CDefaultWorker* mDefaultWorker;
  inject CProgramRunner* mProgramRunner;

  public void work() {
    MProgramResult* programResult = mProgramRunner.run();
    thread.send(programResult);
  }

  public void start() reveal {
    mDefaultWorker.startWorker(this);
  }

  public boolean hasStarted() reveal {
    return mDefaultWorker.hasStarted();
  }

  public boolean hasResult() reveal {
    return mDefaultWorker.hasResult();
  }

  public boolean wasCancelled() reveal {
    return mDefaultWorker.wasCancelled();
  }

  public ::wisey::model* awaitResult() reveal {
    return mDefaultWorker.awaitResult();
  }

  public void cancel() reveal {
    mDefaultWorker.cancel();
  }

  public ::wisey::model* send(::wisey::model* message) {
    return mDefaultWorker.send(message);
  }

  public CCallStack getCallStack() {
    return mDefaultWorker.getCallStack();
  }
}
