package wisey.lang;

external ::llvm::i64 wProcessorCount;

model MSystem {

  /**
   * Returns the original object's type name
   *
   * If the object is an interface returns the concrete object that implements that interface
   */
  public static string getOriginalObjectTypeName(::wisey::object object) {
    return (string) ::llvm::function::__getOriginalObjectName(object);
  }

  /**
   * Returns system configuration value for the number of processors
   */
  public static long getProcessorCount() {
    return ::llvm::global::wProcessorCount;
  }

  /**
   * Returns a pseudo-random number in the given range
   */
  public static int getRandomNumber(int from, int to) {
    return ::llvm::function::get_random_number(from, to);
  }

  /**
   * Forwards the call to memcopy function for needed architecture
   */
  public static void copyMemory(::llvm::i8::pointer destination, 
                                ::llvm::i8::pointer source, 
                                ::llvm::i64 size) {
    ::llvm::function::"llvm.memcpy.p0i8.p0i8.i64"(destination, source, size, false);
  }

  /**
   * Sets memory pointer to by given pointer and of the given size in bytes to all zeros
   */
  public static void zeroMemory(::llvm::i8::pointer pointer, ::llvm::i64 size) {
    ::llvm::function::"llvm.memset.p0i8.i64"(pointer, (char) 0, size, false);
  }
}